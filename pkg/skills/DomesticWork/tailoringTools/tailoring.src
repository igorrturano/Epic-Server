use uo;
use os;

include "include/client";
include ":itemutils:objtype";
include ":itemutils:canAccess";
include "include/say";

program use_thread(who, thread)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, thread))
    return;
  endif
  if(thread.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  SendSysMessage(who, "Select the loom to use that on.");
  var useon := Target(who);
  if (!useon)
    return;
  endif
  if(!ReserveItem(useon))
    SendSysMessage(who,"That is already in use.");
    return;
  endif
  var obj := useon.objtype;
  if ( (obj < UOBJ_LOOM_START) || (obj > UOBJ_LOOM_END))
    SendSysMessage(who,"You can only use that on a loom.");
    return;
  else
    MakeCloth(who, thread);
  endif
endprogram

function MakeCloth(character, yarn)
  if (!Accessible(character, yarn))
    SendSysMessageEx(character, "Voce nao possui o material!", SSM_FAIL);
    return 0;
  endif
  var whox := character.x;
  var whoy := character.y;
  var obj_type := yarn.objtype;
  var obj_color := yarn.color;
  var prod_type;
  var prod_material;
  case (obj_type)
    0xf94:
      //algodao
      prod_type := 0x1a89; //roloalgodao
      break;
    0x1a93:
      //lã
      prod_type := 0x1a92; //rolola
      break;
    0x1a95:
      //fiolinho
      prod_type := 0x1a90; //rololinho
      break;
    0x1a94:
      //fioseda
      prod_type := 0x1a91; //roloseda
      break;
    0x1a96:
      //fiomalva
      prod_type := 0x1a8F; //rolomalva
      break;
    0x1a97:
    //fiosisal
      prod_type := 0x1a8E; //rolosisal
      break;
    0x1a98:
    //fiodenin
      prod_type := 0x1a8D; //rolodenin
      break;
    0x1a99:
    //fiorami
      prod_type := 0x1a8C; //rolorami
      break;
    0x1a9a:
    //fioflamel
      prod_type := 0x1a8B; //roloflamel
      break;
    0x1a9b:
    //fiorajal
      prod_type := 0x1a8A; //rolorajal
      break;
    default:
      SendSysMessage (character, "Este item so serve para tecer.");
      return 0;
  endcase

  if(yarn.amount < 5)
    SendSysMessageEx(character, "Voce nao possui mais material suficiente.", SSM_FAIL);
    return 0;
  endif

  PrintText(character, "*Começou a tear algo*");
  while(yarn.amount >= 5)
    if (character.x != whox || character.y != whoy)
      SendSysMessageEx(character, "Voce se moveu e parou de tear!", SSM_FAIL);
      return 0;
    endif
    if(SubtractAmount(yarn, 5))
      PlaySoundEffect(character, 0x49);
      sleep(3);
      CreateItemInBackpack(character, prod_type, 1);
    endif
  endwhile
  return 1;
endfunction
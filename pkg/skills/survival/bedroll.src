/* $Id: bedroll.src 905 2005-11-05 08:20:50Z muaddiblsd $
 *
 */

use uo;
use os;

include "include/say";
include ":itemutils:canAccess";

CONST LOGOUT_WAIT 	:= 15;
CONST UOBJECT_CAMPFIRE  := 3555;

program use_Bedroll( who, bedroll )

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");
	if(!can_access(who, bedroll))
		return 0;
	endif
	if(bedroll.movable == 0)
		SendSysMessage(who, "Voce nao pode usar isto.");
		return 0;
	endif
	var items := ListItemsNearLocationOfType( who.x,who.y,who.z, 2, UOBJECT_CAMPFIRE, who.realm );
	if( len(items) == 0 )
		SendSysMessage( who, "Voce precisa de um acampamento proximo para usar isto." );
		return 0;
	else
		SendSysMessage( who, "Voce deve permanecer aqui e em paz para preparar o acampamento." );
	endif

	var x := who.x;
	var y := who.y;
	var z := who.z;

	var ttl := LOGOUT_WAIT;

	while( ttl > 0 )
		if( (who.x != x) || (who.y != y) || (who.z != z) )
			SendSysMessage( who, "Voce abandonou suas preparacoes para o acampamento.");
			return 0;
		endif
		var nearbypeople := ListMobilesNearLocation( x ,y, z, 6);
		foreach char in (nearbypeople)
			if (char.warmode)
				PrintTextabove(who, "Algo atrapalha o seu descanso.");
				return;
			endif
		endforeach
		SendSysMessage( who, "Voce deve esperar " + ttl + " segundos para deslogar." );
		ttl := ttl - 5;
		sleep(5);
	endwhile
	
	var bedhp := bedroll.hp;
	var bedhp2 := bedhp -1;
	bedroll.hp := Cint(bedhp2);
	if (bedroll.hp == 0)
	Destroyitem(bedroll);
	SendSysMessageEx(who, "O colchonete ficou imprestavel apos tanto uso!", SSM_FAIL);
	sleepms(50);
	endif
	
	SetObjProperty(who,"#camped",1);
	DisconnectClient( who );

endprogram

/* $Id: anatomy.src 834 2005-11-02 14:09:50Z austin $
 *
 */
use uo;

include ":charactercreation:resists";
include ":attributes:attributeConstants";
include ":attributes:skills";
include ":attributes:attributes";
include ":attributes:skillCheck";
include "include/say";

program skill_Anatomy(who)

	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");

	SendSysMessageEx(who, "Quem voce deseja examinar?", SSM_REQUEST);
	var subject := Target(who);
	// target checks begin here
	if(!subject)
		return 0;
	elseif(subject.graphic == 0x2006)
		PrintText(who, "*examinando o corpo*");
		if (temHabilidade(who, "Formacao Academica"))
			if (GetObjProperty(subject, "morto") == 1)
				SendSysMessageEx(who, "Parece morto para voce.", SSM_INFO);
				if( SkillCheck(who, MEDICINE, 50) > 0 )
					if (GetObjProperty(subject, "WeaponKill"))
						SendSysMessageEx(who, "O corpo foi morto por: " + GetObjProperty(subject, "WeaponKill"), SSM_INFO);
					endif
					if( SkillCheck(who, MEDICINE, 70) > 0 )
						if (GetObjProperty(subject, "deathtimer"))
							SendSysMessageEx(who, "Foi morto a: " + tempoMorto(subject, "deathtimer"), SSM_INFO);
						endif
						if( SkillCheck(who, MEDICINE, 90) > 0 )
							if (GetObjProperty(subject, "deathdetails"))
								SendSysMessageEx(who, "Detalhes: " + GetObjProperty(subject, "deathdetails"), SSM_INFO);
							endif
						endif
					endif
				endif
				return 0;
			else
				var mob := SystemFindObjectBySerial(subject.ownerserial);
				if (mob)
					if (mob.concealed)
						SendSysMessageEx(who, "Parece morto para voce.", SSM_INFO);
						return 0;
					endif
				endif
				SendSysMessageEx(who, "Esta desacordado.", SSM_INFO);
				return 0;
			endif
		else
			SendSysMessageEx(who, "Voce nao tem certeza...", SSM_FAIL);
			return 0;
		endif
	elseif(subject.objtype == 0xEFFF)
		PrintText(who, "*examinando os ossos*");
		if (temHabilidade(who, "Formacao Academica"))
			SendSysMessageEx(who, "Parece inanimado para voce.", SSM_INFO);
			if( SkillCheck(who, MEDICINE, 50) > 0 )
				if (GetObjProperty(subject, "WeaponKill"))
					SendSysMessageEx(who, "O corpo foi morto por: " + GetObjProperty(subject, "WeaponKill"), SSM_INFO);
				endif
				if( SkillCheck(who, MEDICINE, 70) > 0 )
					if (GetObjProperty(subject, "deathtimer"))
						SendSysMessageEx(who, "Foi morto a: " + tempoMorto(subject, "deathtimer"), SSM_INFO);
					endif
					if( SkillCheck(who, MEDICINE, 90) > 0 )
						if (GetObjProperty(subject, "enterrado"))
							SendSysMessageEx(who, "Foi enterrado a: " + tempoMorto(subject, "enterrado"), SSM_INFO);
						endif
						if (GetObjProperty(subject, "deathdetails"))
							SendSysMessageEx(who, "Detalhes: " + GetObjProperty(subject, "deathdetails"), SSM_INFO);
						endif
					endif
				endif
			endif
			return 0;
		else
			SendSysMessageEx(who, "Voce nao tem certeza...", SSM_FAIL);
			return 0;
		endif
		return 0;
	elseif(!subject.isA(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Alvo invalido.", SSM_FAIL);
		return 0;
	endif

	// variables for sys messages
  	var streng := getStreng(AP_GetStat(subject, "Strength"));
	var dext := getDext(AP_GetStat(subject, "Dexterity"));

	var fort := getFort(GetResist(subject, FORTITUDE));
	var refl := getRefl(GetResist(subject, REFLEXOS));
	var vont := getVont(GetResist(subject, VONTADE));
	//var con := getVit( Cint(GetObjProperty(who, "Vitality") ));
	var endur_percentage := CInt(CInt(AP_GetVital(subject, "Stamina"))/CDbl(AP_GetVitalMaximumValue(subject, "Stamina"))*100);
	var endur := getEnd(endur_percentage);


	var strMsg, strFail;

	if(subject.serial == who.serial)
		strMsg := "-se";
	else
		strMsg := " "+subject.name;
		strFail := "das caracteristicas fisicas dessa criatura.";
	endif

	PrintTextAbove(who, "*examinando" + strMsg + "*");
	if (!Wait(subject, 2))
		SendSysMessageEx(who, "O paciente precisa ficar preparado.", SSM_FAIL);
		return 0;
	endif
	var hunger := CInt(AP_GetVital(subject, "Hunger") / 10);
	PrintTextAbovePrivate(subject, "Niveis de fome: " + hunger , who);

	var sucesso := 0;
	if (who.serial == subject.serial) sucesso := 1; //PARA O PLAYER SEMPRE ACERTAR E NUNCA UPAR, CASO USE NELE MESMO
	else sucesso := SkillCheck(who, MEDICINE, 30);
	endif

	if (temHabilidade(who, "Formacao Academica"))
		if(sucesso > 0)
			SendSysMessageEx(who, strMsg + " parece " + streng +" (" +AP_GetStat(subject, "Strength") + "),", SSM_INFO);
			SendSysMessageEx(who, dext +" (" + AP_GetStat(subject, "Dexterity") + "),", SSM_INFO);
			SendSysMessageEx(who, endur + ".", SSM_INFO);
			SendSysMessageEx(who, fort + " (" + GetResist(subject, FORTITUDE) + "),", SSM_INFO);
			SendSysMessageEx(who, refl + " (" + GetResist(subject, REFLEXOS) + "),", SSM_INFO);
			SendSysMessageEx(who, vont + " (" + GetResist(subject, VONTADE) + "),", SSM_INFO);
            if (who.serial == subject.serial)
			    SendSysMessageEx(who, "O alvo esta " + DPMsg(subject,who), SSM_INFO);
            endif
			return 1;
		else
			SendSysMessageEx(who, "Voce nao faz ideia " + strFail, SSM_FAIL);
			return 0;
		endif
	else
		if(sucesso > 0)
			SendSysMessageEx(who, strMsg + " parece " + streng + ", " + dext + " e " + endur + ".", SSM_INFO);
			if ((valorInicial(who, MEDICINE) >= 40) || (who.serial == subject.serial))
				SendSysMessageEx(who, strMsg + " parece " + fort + ", " + refl + " e "+ vont + ".", SSM_INFO);
			endif
            if (who.serial == subject.serial)
			   SendSysMessageEx(who, "O alvo esta " + DPMsg(subject,who), SSM_INFO);
			elseif (GetObjProperty(subject, "bond") == "fiel")
			   if (AP_Getvital(subject, "DP") == 20)
				SendSysMessageEx(who, "O alvo esta bastante disposto", SSM_INFO);
			   else
				SendSysMessageEx(who, "O alvo esta quase morto", SSM_INFO);
				endif
			endif
            return 1;
		else
			SendSysMessageEx(who, "Voce nao faz ideia " + strFail, SSM_FAIL);
			return 0;
		endif
	endif
endprogram

function getStreng(subject)
	if (subject >= 1000)
		return "ter forca para mover montanhas";
	elseif(subject >= 600)
		return  "ter a forca de um exercito";
	elseif(subject >= 500)
		return  "ter forca para derrubar uma muralha";
	elseif(subject >= 400)
		return  "ter forca para derrubar uma arvore";
	elseif(subject >= 300)
		return  "ter forca de 5 homens";
	elseif(subject >= 200)
		return  "ter forca de um gigante";
	elseif(subject >= 100)
		return  "ter forca sobre-humana";
	elseif(subject >= 99)
		return "um dos mais fortes que voce ja viu";
	elseif(subject >= 89)
		return "forte como um touro";
	elseif(subject >= 79)
		return "extraordinariamente forte";
	elseif(subject >= 69)
		return "extrememamente forte";
	elseif(subject >= 59)
		return "muito forte";
	elseif(subject >= 49)
		return "um pouco forte";
	elseif(subject >= 39)
		return "ter uma forca normal";
	elseif(subject >= 29)
		return "razoavelmente fraca";
	elseif(subject >= 19)
		return "muito fraca";
	else
		return "ter problemas para erguer ate pequenso objetos";
	endif

endfunction


function getDext(subject)


	if(subject >= 100)
		return "sobre-humananmente agil";
	elseif(subject >= 99)
		return "uma das pessoas mais rapidas que voce ja viu";
	elseif(subject >= 89)
		return "agil como mercurio";
	elseif(subject >= 79)
		return "extraordinariamente agil";
	elseif(subject >= 69)
		return "extremamente agil";
	elseif(subject >= 59)
		return "muito agil";
	elseif(subject >= 49)
		return "um pouco agil";
	elseif(subject >= 39)
		return "moderadamente agil";
	elseif(subject >= 29)
		return "meio descoordenado";
	elseif(subject >= 19)
		return "muito pouco agil";
	else
		return "mal consegue se mover";
	endif

endfunction

function getVit(subject)


	if(subject >= 100)
		return "parece ter saude sobre-humana";
	elseif(subject >= 99)
		return "uma das pessoas mais saudaveis que voce ja viu";
	elseif(subject >= 89)
		return "parece que nunca deve ficar doente";
	elseif(subject >= 79)
		return "extraordinariamente saudavel";
	elseif(subject >= 69)
		return "extremamente saudavel";
	elseif(subject >= 59)
		return "muito saudavel";
	elseif(subject >= 49)
		return "um pouco saudavel";
	elseif(subject >= 39)
		return "moderadamente saudavel";
	elseif(subject >= 29)
		return "parece ter a saude comprometida";
	elseif(subject >= 19)
		return "parece doentio";
	else
		return "";
	endif

endfunction

function getEnd(subject)

	if(subject >= 100)
		return "com disposicao completa";
	elseif(subject >= 90)
		return "com muita disposicao";
	elseif(subject >= 80)
		return "com disposicao";
	elseif(subject >= 70)
		return "com alguma disposicao";
	elseif(subject >= 60)
		return "com pouca disposicao";
	elseif(subject >= 50)
		return "com muita pouca disposicao";
	elseif(subject >= 40)
		return "com quase nenhuma diposicao";
	elseif(subject >= 30)
		return "com cansaco";
	elseif(subject >= 20)
		return "com muito cansaco";
	elseif(subject >= 10)
		return "quase exausto";
	else
		return "totalmente exausto";
	endif

endfunction

function DPMsg(who,medic)
	if (GetObjProperty(who, "morto"))
		return "morto";
	endif
	if(temHabilidade(medic, "Formacao Academica"))
		var DPs := CInt(AP_GetVital(who, "DP") / 10);
		case (DPs)
			0: return "morto (0 DP)";
			1: return "quase morto (1 DP)";
			2: return "mal (2 DP)";
			3: return "relativamente bem (3 DP)";
			4: return "bastante disposto (4 DP)";
			5: return "preparado para tudo (5 DP)";
			6: return "mais do que preparado (6 DP)";
			7: return "bem mais do que preparado (7 DP)";
			8: return "extremamente preparado (8 DP)";
			default: return "extremamente preparado ("+ DPs +" DP)";
		endcase
	elseif(who.serial == medic.serial)
		var DPs := CInt(AP_GetVital(who, "DP") / 10);
		case (DPs)
			0: return "morto";
			1: return "quase morto";
			2: return "mal";
			3: return "relativamente bem";
			4: return "bastante disposto";
			5: return "preparado para tudo";
			6: return "mais do que preparado";
			7: return "bem mais do que preparado";
			8: return "extremamente preparado";
			default: return "extremamente preparado";
		endcase
	else
		var FakeDPs := CInt(AP_GetVital(who, "DP") / 10) - CInt(GetObjProperty(who, "dpmod")/10);
		case (FakeDPs)
			0: return "morto";
			1: return "quase morto";
			2: return "mal";
			3: return "relativamente bem";
			4: return "relativamente bem";
			5: return "bastante disposto";
			default: return "quase morto";
		endcase
	endif
endfunction


//------------------------------------------------------
//DIFICULDADES REFERÊNCIA PARA OS TESTES DE RESISTÊNCIA
//
//Fácil 				30
//Médio 				45
//Difícil 				60
//Muito Difícil 		75
//Extremamente Difícil 	90
//Quase Impossível 		105
//
//------------------------------------------------------

function getFort(subject)
	if (subject >= 105)
		return "ter o imunologico de um deus";
	elseif(subject >= 90)
		return "ter o imunologico de um semi-deus";
	elseif(subject >= 75)
		return "ter o imunologico excepcional";
	elseif(subject >= 60)
		return "ter o imunologico muito forte";
	elseif(subject >= 45)
		return "ter o imunologico forte";
	elseif(subject >= 30)
		return "ter o imunologico mediano";
	elseif(subject >= 15)
		return "ter o imunologico fragil";
	elseif(subject >= 0)
		return "ter o imunologico muito fragil";
	else
		return "nem ter um sistema imunologico";
	endif
endfunction

function getRefl(subject)
	if (subject >= 105)
		return "ter reflexos de um deus";
	elseif(subject >= 90)
		return "ter reflexos de um semi-deus";
	elseif(subject >= 75)
		return "ter reflexos excepcionalmente rapidos";
	elseif(subject >= 60)
		return "ter reflexos muito rapidos";
	elseif(subject >= 45)
		return "ter reflexos rapidos";
	elseif(subject >= 30)
		return "ter reflexos medianos";
	elseif(subject >= 15)
		return "ter reflexos lentos";
	elseif(subject >= 0)
		return "ter reflexos muito lentos";
	else
		return "nao ter reflexo algum";
	endif
endfunction

function getVont(subject)
	if (subject >= 105)
		return "ser perspicaz como um deus";
	elseif(subject >= 90)
		return "ser perspicaz como um semi-deus";
	elseif(subject >= 75)
		return "ser excepcionalmente perspicaz";
	elseif(subject >= 60)
		return "ser muito perspicaz";
	elseif(subject >= 45)
		return "ser perspicaz";
	elseif(subject >= 30)
		return "ter uma perspicacia mediocre";
	elseif(subject >= 15)
		return "ser pouco perspicaz";
	elseif(subject >= 0)
		return "ser muito pouco perspicaz";
	else
		return "ser nada perspicaz";
	endif
endfunction



exported function tempoMorto(character, tipo)// para 10000 deu 292 segundos 300
    var criadoEm := cint(GetObjProperty(character, tipo));
    var tempoAgora := polcore().systime;// segundos
    var tempoDevida:= tempoAgora - criadoEm;
    var dias := cint(tempoDevida/(86400));//24horas*60minutos*60segundos = 86400 segundos
	if (dias <= 2)
		dias := "Ha pouco tempo";
	elseif (dias > 2 && dias <= 8)
		dias := "Ha algum tempo";
	elseif (dias > 8 && dias <= 15)
		dias := "Ha muito tempo";
	else
		dias := "Ha um ano ou mais";
	endif
    return dias;
endfunction

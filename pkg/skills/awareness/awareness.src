/* $Id: hiding.src 974 2007-01-18 10:52:17Z sroyalty $
 *
 */

use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":awareness:awareness";

program skill_Alertness(who)
	if ( CInt(GetobjProperty(who, "#Alertness")) > ReadGameClock() )
		SendSysMessageEx(who, "Voce precisa esperar para usar essa pericia novamente.", SSM_INFO);
		return;
	else
		SetObjProperty(who, "#Alertness", ReadGameClock()+1);
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#IsWaiting");

	var deveCheckarLOS := 0;
	var isLOS := 0;
	var isEncontrado := 0;
	var ignore := GetObjProperty(who, "IgnoreHidden");
	SendSysMessageEx(who, "Selecione um alvo. Cancelar o alvo procurará algo em volta.", SSM_INFO);
	SendSysMessageEx(who, "Selecionar a si mesmo Ativa ou Desativa a procura passiva. [Atual: {}]".format(ignore? "Ativado.": "Desativado."), SSM_INFO);
	var targ := Target(who,TGTOPT_NOCHECK_LOS);

	if (targ.isA(POLCLASS_MOBILE))
		deveCheckarLOS := true;
		isLOS := CheckLosBetween(who.x, who.y, who.z, targ.x, targ.y, targ.z, who.realm);
		if(isLOS)
			if(targ == who)
				AtivaDesativaPassivo(who);
				isEncontrado := 1;
			else
				isEncontrado := ExaminarMobile(who,targ);
			endif
		endif
	elseif(targ.isA(POLCLASS_CONTAINER) || targ.isA(POLCLASS_DOOR))
		deveCheckarLOS := true;
		isLOS := CheckLosBetween(who.x, who.y, who.z, targ.x, targ.y, targ.z, who.realm);
		if(isLOS)
			isEncontrado := ExaminarObjeto(who,targ);
		endif
	elseif(targ == 0)
		isEncontrado := ExaminarArea(who);
	else
		SendSysMessageEx(who, "Alvo inválido.", SSM_FAIL);
	endif

	if(deveCheckarLOS && !isLOS)
		SendSysMessageEx(who, "Você não enxerga o alvo.", SSM_FAIL);
	endif
endprogram

/* $Id: hiding.src 974 2007-01-18 10:52:17Z sroyalty $
 *
 */

use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":awareness:awareness";

program skill_Alertness(who)
	if ( CInt(GetobjProperty(who, "#Alertness")) > ReadGameClock() )
		SendSysMessageEx(who, "Voce precisa esperar para usar essa pericia novamente.", SSM_INFO);
		return;
	else
		SetObjProperty(who, "#Alertness", ReadGameClock()+1);
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#IsWaiting");

	var menu := choose(who);
	if (menu == 1)
		if (!examinarAlvo(who))
			SendSysMessageEx(who, "Voce nao encontrou nada de diferente.", SSM_INFO);
			return 1;
		endif
	elseif (menu == 2)
		if(who.hidden)
			SendSysMessageEx(who, "Procurando algo", SSM_INFO);
		else
			PrintTextAbove(who, "*Procurando algo*");
		endif
		if ( !examinarArea(who) )
			SendSysMessageEx(who, "Voce nao encontrou nada de diferente.", SSM_INFO);
			return 1;
		endif
	else
		var ignore := GetObjProperty(who, "IgnoreHidden");
		if (ignore)
			SendSysMessageEx(who, "Modo Passivo Ligado. Procure e revela passivamente!", SSM_INFO);
			EraseObjProperty(who, "IgnoreHidden");
		else
			SendSysMessageEx(who, "Modo Passivo Desligado. Percebe, mas não revela passivamente!", SSM_FAIL);
			SetObjProperty(who, "IgnoreHidden", 1);
		endif
	endif
endprogram

function Choose(mobile)
	var yn_gump := GFCreateGump(140, 100);

	GFClosable(yn_gump, 0);
	GFResizePic(yn_gump, 0, 0, GFCfgConst("Defaults", "BackGround"), 400, 180); //205);
	GFResizePic(yn_gump, 15, 15, GFCfgConst("Defaults", "ForeGround"), 370, 150); //175);

	var y_pos := 20;
	GFTextLine(yn_gump, 20, y_pos, 1153, "Escolha uma opção.");
	y_pos := y_pos+40;
	GFAddButton(yn_gump, 20, (y_pos+3), 2117, 2118, GF_CLOSE_BTN, 1);
	GFTextLine(yn_gump, 40, y_pos, 1153, "Examinar algo específico.");
	y_pos := y_pos+40;
	GFAddButton(yn_gump, 20, (y_pos+3), 2117, 2118, GF_CLOSE_BTN, 2);
	GFTextLine(yn_gump, 40, y_pos, 1153, "Examinar a área que estou.");
	y_pos := y_pos+40;
	GFAddButton(yn_gump, 20, (y_pos+3), 2117, 2118, GF_CLOSE_BTN, 3);
	var ignore := GetObjProperty(mobile, "IgnoreHidden");
	var onoff := "Desligar";
	if (ignore)
		onoff := "Ligar";
	endif

	GFTextLine(yn_gump, 40, y_pos, 1153, onoff+" Passividade");

	var input := GFSendGump(mobile, yn_gump);
	input := input[0];
	return input;
endfunction

use os;
use uo;
use util;
use cfgfile;
include ":alchemy:alchemy";
include ":attributes:attributes";
include ":timedScripts:timedScripts";
include "include/say";

program drink_blue (who, potion)
	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");
	if(!Accessible(who, potion))
		return;
	endif
	if(potion.movable == 0)
		SendSysMessageEx(who, "Voce nao pode beber isto.", SSM_FAIL);
		return;
	endif
	if (cint(GetObjProperty(who, "#staminapotion")))
		SendSysMessageEx(who, "Voce ja esta sob efeito de uma pocao igual.", SSM_FAIL);
		return;
	endif


	if ( TS_GetCooldown(who, "staminapot") != error)
		SendSysMessageEx(who, "Voce nao pode usar essa pocao agora.", SSM_FAIL);
		return 1;
	endif

	var healed := 0;
	healed := AP_GetVitalMaximumValue(who, STAMINA);
	if(potion.objtype == 0xdc0f)
		healed := cint(healed*0.7);
	else
		healed := cint(healed*0.3);
	endif
	empty_bottle(who, potion);
	TS_StartCooldown(who, "staminapot", cint(healed*0.5)+1);

	Detach();
	var i := 0;
	SetObjProperty(who, "#staminapotion", 1);
	while (i < healed)
		if (AP_GetVital(who, STAMINA) > AP_GetVitalMaximumValue(who, STAMINA))
			break;
		endif
		AP_SetVital(who, STAMINA, AP_GetVital(who, STAMINA)+1 );
		i := i + 1;
		sleepms(500);
	endwhile
	EraseObjProperty(who, "#staminapotion");
	SendSysMessageEx(who, "Terminou o efeito da pocao.", SSM_INFO);	
endprogram

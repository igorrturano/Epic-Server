use os;
use uo;
use util;
use cfgfile;

include ":alchemy:alchemy";
include ":spells:spell_logic/swordmage/include/encantarObjeto";

program drink_blue( who, potion )
  EraseObjProperty( who, "IsMeditating" );
  EraseObjProperty( who, "HealTimer" );

  if ( !Accessible( who, potion ) )
    return;
  endif

  if ( potion.movable == 0 )
    SendSysMessage( who, "Voce nao pode usar isto." );
    return;
  endif

  SendSysMessageEx( who, "Qual item deseja encantar?", SSM_REQUEST );
  var use_on := Target( who );

  if ( !use_on )
    SendSysMessage( who, "Cancelado" );
    return 0;
  endif

  if ( !use_on.isA( POLCLASS_WEAPON ) && !use_on.isA( POLCLASS_ARMOR ))
    SendSysMessageEx( who, "Isto não pode ser banhado por essa poção.", SSM_FAIL );
    return;
  endif

  var bonus := Cint( GetObjProperty(potion, "crafterskill" ) );
	bonus := bonus * 0.05;

  DestroyItem(potion);

  SendSysMessageEx( who, "Você encantou o objeto.", SSM_INFO );
  PlayStationaryEffect( who.x, who.y, who.z, FX_SPARK_EFFECT, 2, 2 );
  PlaySoundEffect( who, SFX_SPELL_HEAL );
  setBonus( who, use_on, bonus );
endprogram
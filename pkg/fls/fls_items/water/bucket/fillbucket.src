use uo;
use os;
use cfgfile; 
include ":water:buckets";
include "include/say";
var config := ReadConfigFile("water");

const SPLASH_SOUND	:= 0x26;

program FillBucket(who, bucket)
	if ( !ReserveItem(bucket) )
		SendSysMessage(who, "Alguem ja esta usando isto.");
		return;
	endif
	
	SendSysMessage(who, "Onde voce deseja usar isto?");
	var targ := TargetCoordinates(who);
	
	if ( targ.mobile )
		MilkIt(who, targ.mobile, bucket);
	else
		GetWater(who, targ, bucket);
	endif
endprogram

function MilkIt(who, mobile, bucket)
	if ( Distance(who, mobile) > 1 )
		SendSysMessage(who, "O alvo esta muito distante.");
		return;
	elseif ( CInt(GetObjProperty(mobile, "#MilkWait")) > ReadGameClock() )
		SendSysMessage(who, "A femea esta sem leite.");
		return;
	endif
	
	var cows := array(":brainai:cow", ":brainai:cow2", ":brainai:cabra");
	var template := lower(mobile.npctemplate);
	if ( template in cows )
		SetObjProperty(mobile, "#MilkWait", ReadGameClock()+90);
		if (mobile.npctemplate == ":brainai:cabra")
		PlaySoundEffect(mobile, 0x099);
		else
		PlaySoundEffect(mobile, 0x7A);
		endif
		sleep(3);
		if (mobile.npctemplate == ":brainai:cabra")
			PlaySoundEffect(mobile, 0x09D);
		else
			PlaySoundEffect(mobile, 0x7B);;
		endif
		sleep(3);
		if (mobile.npctemplate == ":brainai:cabra")
			PlaySoundEffect(mobile, 0x099);
		else
			PlaySoundEffect(mobile, 0x7A);
		endif
		SendSysMessage(who, "Voce encheu o balde de leite.");
		
		ReleaseItem(bucket);
		var new_bucket := bucket.FillBucket(MILK_BUCKET);
		if ( new_bucket.errortext )
			SendSysMessage(who, "Unable to create filled bucket ->"+new_bucket.errortext);
		endif
	else
		SendSysMessage(who, "Esta criatura nao da leite.");
		return;
	endif
endfunction

function GetWater(who, targ, bucket)
	if ( !IsWater(targ) )
		SendSysMessage(who, "Esta nao e uma boa fonte de agua!");
		return;
	elseif ( CoordinateDistance(who.x, who.y, targ.x, targ.y) > 3 )
		SendSysMessage(who, "O alvo esta muito distante.");
		return;
	endif

	var fresh_regions := GetConfigStringArray(config["FreshWater"], "rect");
	var is_fresh := 0;
	foreach region in fresh_regions
		region := SplitWords(region);
		var x1 := CInt(region[1]);
		var y1 := CInt(region[2]);
		var x2 := CInt(region[3]);
		var y2 := CInt(region[4]);

		if ( ( targ.x >= x1 && targ.y >= y1 && targ.x <= x2 && targ.y <= y2 ) )
			is_fresh := 1;
			break;
		endif
	endforeach

	if (CStr(targ.item.objtype) in GetConfigStringArray(config["WaterTiles"], "staticWater"))
		var waternumber := GetObjProperty(targ.item, "water");
		if (waternumber <= 0)
			SendSysMessageEx(who, "A agua deste balde esta muito suja ou e escassa", SSM_FAIL);
			return;
		endif
		waternumber := waternumber -1;
		SetObjProperty(targ.item, "water", waternumber);
		if (waternumber <= 0)
			if (targ.item.graphic == 3707)
				EmptyBalde(targ.item);
			endif
		endif
	endif
	
	PlaySoundEffect(who, SPLASH_SOUND);
	if (!CStr(targ.objtype) in GetConfigStringArray(config["WaterTiles"], "staticWater"))
		PlayStationaryEffect(targ.x, targ.y, targ.z, 13613, 10, 10, 1);
	endif
	
	var new_bucket;
	if ( is_fresh )
		SendSysMessage(who, "Voce encheu o balde com agua doce.");
		ReleaseItem(bucket);
		new_bucket := bucket.FillBucket(FRESH_WATER_BUCKET);
	else
		SendSysMessage(who, "Voce encheu o balde com agua salgada.");
		ReleaseItem(bucket);
		new_bucket := bucket.FillBucket(SALT_WATER_BUCKET);
	endif
	
	if ( new_bucket.errortext )
		SendSysMessage(who, "Unable to create filled bucket ->"+new_bucket.errortext);
	endif
endfunction

function EmptyBalde(bucket)
	var info := struct;
	info.+container := bucket.container;
	info.+color := bucket.color;
	info.+x := bucket.x;
	info.+y := bucket.y;
	info.+z := bucket.z;
	
	ReleaseItem(bucket);

	var new_bucket;
	if ( info.container )
		new_bucket := CreateItemInContainer(info.container, 3715, 1);
	else
		new_bucket := CreateItemAtLocation(info.x, info.y, info.z, 3715, info.realm);
	endif
	SetObjProperty(new_bucket, "canfix", GetObjProperty(bucket, "canfix"));
	SetObjProperty(new_bucket, "crafterserial", GetObjProperty(bucket, "crafterserial"));
	SetObjProperty(new_bucket, "fixer", GetObjProperty(bucket, "fixer"));
	SetObjProperty(new_bucket, "water", 0);
	new_bucket.color := info.color;
	DestroyItem(bucket);
	return new_bucket;
endfunction

function IsWater(targ)
	var tiles := config["WaterTiles"];
	var land_scape := CStr( GetMapInfo(targ.x, targ.y).landtile );
	if ( targ.objtype )
		if ( CStr(targ.objtype) in GetConfigStringArray(tiles, "static") )
		 	return 1;
		endif
	elseif ( land_scape in GetConfigStringArray(tiles, "landscape") )
		return 1;
	endif
	if (targ.item.objtype)
		if ( CStr(targ.item.objtype) in GetConfigStringArray(tiles, "staticWater") )
		 	return 1;
		endif
	endif
	
	return 0;
endfunction

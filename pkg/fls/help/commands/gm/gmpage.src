use uo;
use datafile;

include ":datafile:datafile";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":help:help";
include ":gumps:htmlGump";
include ":attributes:advanceCheck";

program gmpage(who)

	GmPageGump(who);

endprogram

function GMPageGump(who)

	var pagegump := GFCreateGump();
	var input;

	GFDisposable(pagegump, 0);
	GFResizePic(pagegump, 0, 25, 3600, 525, 480);
	GFResizePic(pagegump, 15, 40, 0x13BE, 495, 450);
	GFTextMid(pagegump, 20, 50, 370, 1153, "Menu de Pages");

	GFTextLine(pagegump, 20, 70, 1153, "Nome");
	GFTextLine(pagegump, 160, 70, 1153, "Goto");
	GFTextLine(pagegump, 200, 70, 1153, "Msg");
	GFTextLine(pagegump, 240, 70, 1153, "Summon");
	GFTextLine(pagegump, 300, 70, 1153, "Descricao");
	GFTextLine(pagegump, 370, 70, 1153, "Deletar");
	GFTextLine(pagegump, 445, 70, 1153, "Tempo");

	var df := DFOpenDataFile("gmpages", DF_CREATE);
	var pages := df.Keys();

	var y := 90;
	var i := 1;
	var pagenum := 0;
	var elem;
	var player;
	var fontcolor := 1153;
	foreach page in pages
		pagenum := pagenum + 1;
		GFPage(pagegump, i);
		elem := df.FindElement( page );
		player := SystemFindObjectBySerial(Cint(page));
		var getdate := DFFindElement(DFOpenDataFile("gmpages", DF_CREATE, DF_KEYTYPE_INTEGER), cint(page), DF_CREATE);
		GFTextLine(pagegump, 20, y, fontcolor, player.name+" ("+player.acctname+")");
		GFAddButton(pagegump, 165, y, 2117, 2118, 1, 200 + pagenum);
		GFAddButton(pagegump, 205, y, 2117, 2118, 1, 400 + pagenum);
		GFAddButton(pagegump, 255, y, 2117, 2118, 1, 600 + pagenum);
		GFAddButton(pagegump, 320, y, 2117, 2118, 1, 800 + pagenum);
		GFAddButton(pagegump, 380, y, 2117, 2118, 1, 1000 + pagenum);
		GFTextLine(pagegump, 442, y, fontcolor, CalculateDaysInReal(getdate.GetProp("date"))+" dias");
		y := y + 20;
		if ( Cdbl(pagenum/18) == i)
			i := i +1;
			y := 90;
			GFAddButton(pagegump, 445, 460, 2648, 2649, GF_PAGE_BTN, i);
			GFPage(pagegump, i);
			GFAddButton(pagegump, 445, 50, 2650, 2651, GF_PAGE_BTN, i-1);
		endif
	endforeach


	input := GFSendGump(who, pagegump);
	input := input[0];

	if ( (input > 200) && (input < 400) )
		var serial := GetPageIndexByPageNum(input - 200, "gmpages");
		if ( serial )
			var player := SystemFindObjectBySerial(Cint(serial));
			if (player == error)
				SendSysMessage(who, "Esse personagem foi deletado!");
			else
				MoveObjectToLocation(who, player.x, player.y, player.z, player.realm);
			endif
		else
			SendSysMessage(who, "Essa page foi deletada.");
		endif
		GMPageGump(who);
	elseif ( (input > 400) && (input < 600) )
		var serial := GetPageIndexByPageNum(input - 400, "gmpages");
		if ( serial )
			var player := SystemFindObjectBySerial(Cint(serial));
			if (player == error)
				SendSysMessage(who, "Esse personagem foi deletado!");
			else
				var message_txt := RequestInput(who, who.backpack, "Digite sua mensagem: ");
				if ( !message_txt )
					SendSysMessage(who, "Cancelado.");
					return 0;
				else
					SendSysMessage(who, "Resposta enviada para " + player.name + " .");
					SendSysMessage(player, "Staff: " +  message_txt);
				endif;
			endif
		else
			SendSysMessage(who, "Essa page foi deletada.");
		endif
		GMPageGump(who);
	elseif ( (input > 600) && (input < 800) )
		var serial := GetPageIndexByPageNum(input - 600, "gmpages");
		if ( serial )
			var player := SystemFindObjectBySerial(Cint(serial));
			if (player == error)
				SendSysMessage(who, "Esse personagem foi deletado!");
			else
				MoveObjectToLocation(player, who.x, who.y, who.z, who.realm);
			endif
		else
			SendSysMessage(who, "Essa page foi deletada.");
		endif
		GMPageGump(who);
	elseif ( (input > 800)  && (input < 1000) )
		var serial := GetPageIndexByPageNum(input - 800, "gmpages");
		if ( serial )
			var player := SystemFindObjectBySerial(Cint(serial));
			var df := DFOpenDataFile("gmpages", DF_CREATE, DF_KEYTYPE_INTEGER);
			var elem := DFFindElement(df, cint(serial), DF_CREATE);
			if (elem == error)
				SendSysMessage(who, "Essa page foi deletada.");
			else
				PageDescGump(who, elem);
			endif
		else
			SendSysMessage(who, "Essa page foi deletada.");
		endif
		GMPageGump(who);
	elseif ( (input > 1000) )
		var serial := GetPageIndexByPageNum(input - 1000, "gmpages");
		DeletePage( serial, "gmpages");
		GMPageGump(who);
	endif

endfunction
/*
 * O comando checa (e cria/remove) uma CProp temporária com o nome passado no debugName
 * Modelo da CProp: #debug_<string>
 * Exemplo da CProp: #debug_clima
 *
 * Forma de uso:
 * Dar include ":commands:debugsystem" no script que for usar
 * e adicionar a função DebugMsg(who, debugType, msg) dentro do script
 * Exemplo de uso:
 * 
 * include ":commands:debugsystem";
 *
 * var debugType := "clima";
 * DebugMsg(who, debugType, "Está chovendo bastante!");
 *
 * Para scripts que não tenha who (Ref de Char) usar DebugMsgEvent(debugType, msg);
 * Exemplo de uso:
 *
 * var debugType := "clima";
 * DebugMsgEvent(debugType, "Está chovendo bastante!");
 *
 * É preciso certificar que o nome do debug esteja na var listDebugs deste arquivo aqui
 * o valor precisa ser um struct com name e level (cmdlevel) [ver uso abaixo]
 * Não fiz com ENUM pra evitar alguns conflitos de varredura.
 */
use uo;

include ":commands:debugsystem";
include "include/say";

program CMD_debug(who, debugName)
	debugName.Lower();
	
    if (!debugName || Lower(debugName) == "list")
		SendSysMessage(who, "Lista de debugs disponíveis: ");
		foreach debug in listDebugs
			SendSysMessage(who, $"Tag: {debug.name} - PLevel: {debug.level}");
		endforeach
		return;
	endif
	
	foreach debug in listDebugs
        if (Lower(debugName) == Lower(debug.name))
            if (checkLv(who, debug.level))
                setDebug(who, debugName);
                return;
            else
                SendSysMessageEx(who, $"DEBUG {debugName}:  Nível insuficiente.");
            endif
        endif
    endforeach

    SendSysMessageEx(who, $"DEBUG {debugName}: inexistente. Confira se a tag está correta.");
	SendSysMessageEx(who, "Para ver os debug disponíveis digite: .debug ou .debug list");
endprogram

function checkLv(who, level)
    if (who.cmdlevel >= level)
        return 1;
    endif
    return 0;
endfunction

function setDebug(who, debugName)
    var propName := PROPTAG + debugName;
    var prop := getDebugProp(who, debugName);

    if (!prop)
        SetObjProperty(who, propName, 1);
        SendSysMessageEx(who, $"DEBUG {debugName}: ativado!");
    else
        EraseObjProperty(who, propName);
        SendSysMessageEx(who, $"DEBUG {debugName}: desativado!");
    endif
endfunction

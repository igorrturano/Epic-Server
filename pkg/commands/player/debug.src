/**
/ O comando checa (e cria/remove) uma CProp temporária com o nome passado no debugName
/ Modelo da CProp: #debug_<string>
/ Exemplo da CProp: #debug_clima
/
/ Forma de uso:
/ Dar include ":commands:debugsystem" no script que for usar
/ e adicionar a função DebugMsg(who, debugType, msg) dentro do script
/ Exemplo de uso:
/ 
/ include ":commands:debugsystem";
/
/ var debugType := "clima";
/ DebugMsg(who, debugType, "Está chovendo bastante!");
/
/ Para scripts que não tenha who (Ref de Char) usar DebugMsgEvent(debugType, msg);
/ Exemplo de uso:
/
/ var debugType := "clima";
/ DebugMsg(debugType, "Está chovendo bastante!");
/
/ É preciso certificar que o nome do debug esteja na var listDebugs deste arquivo aqui
/ o valor precisa ser um struct com name e level (cmdlevel) [ver uso abaixo]
/ Não fiz com ENUM pra evitar alguns conflitos de varredura.
**/
use uo;

include ":commands:debugsystem";
include "include/say";

var listDebugs := array{
    struct { name := "all", "level" := 2 },
    struct { name := "clima", "level" := 1 }, 
    struct { name := "damage", "level" := 1 }
};

program CMD_debug(who, debugName)
    foreach debug in listDebugs
        if (debugName == debug.name)
            if (checkLv(who, debug.level))
                setDebug(who, debugName);
                return;
            else
                SendSysMessageEx(who, "DEBUG " + debugName + ": Nível insuficiente.");
            endif
        endif
    endforeach

    SendSysMessageEx(who, "DEBUG " + debugName + ": inexistente. Confira se a tag está correta.");
endprogram

function checkLv(who, level)
    if (who.cmdlevel >= level)
        return true;
    endif
    return false;
endfunction

function setDebug(who, debugName)
    var propName := PROPTAG + debugName;
    var prop := getDebugProp(who, debugName);

    if (!prop)
        SetObjProperty(who, propName, true);
        SendSysMessageEx(who, "DEBUG " + debugName + ": ativado!");
    else
        EraseObjProperty(who, propName);
        SendSysMessageEx(who, "DEBUG " + debugName + ": desativado!");
    endif
endfunction

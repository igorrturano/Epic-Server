use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include "include/say";

program command_cidadania(who, text)
    if (!who.cmdlevel)
        SendSysMessage(who, "Você não tem permissão para usar este comando.");
        return 0;
    endif

    var targ := Target(who);
    if (!targ)
        SendSysMessage(who, "Cancelado.");
        return 0;
    endif

    if (!targ.acctname)
        SendSysMessage(who, "Você deve selecionar um jogador.");
        return 0;
    endif

    var chardata := GetObjProperty(targ, "chardata");
    if (!chardata)
        SendSysMessage(who, "Erro: chardata não encontrado para o jogador selecionado.");
        return 0;
    endif

    if (!chardata.available_citizenship_titles)
        SendSysMessage(who, "Erro: títulos de cidadania não definidos para este jogador.");
        return 0;
    endif

    var gump := GFCreateGump();
    GFResizePic(gump, 0, 0, GFCfgConst("Defaults", "BackGround"), 300, 450);
    GFResizePic(gump, 10, 10, GFCfgConst("Defaults", "ForeGround"), 280, 430);

    GFTextMid(gump, 10, 20, 280, 2100, "Definir Título de Cidadania");

    // Mostrar o título atual em dourado
    if (chardata.citizenship_title)
        GFTextMid(gump, 10, 50, 280, 53, "Título atual: " + chardata.citizenship_title);
    endif

    var y := 80;
    var i := 1;
    foreach title in (chardata.available_citizenship_titles)
        GFAddButton(gump, 20, y, 2117, 2118, GF_CLOSE_BTN, i);
        GFTextLine(gump, 40, y, 2100, title);
        y += 30;
        i += 1;
    endforeach

    // Adicionar mensagem em vermelho
    GFTextMid(gump, 10, 380, 280, 232, "Não é possível ");
    GFTextMid(gump, 10, 400, 295, 232, "remover um título de cidadania,");
    GFTextMid(gump, 10, 420, 292, 232, "apenas trocar o status de cidadania.");
    var input := GFSendGump(who, gump);
    
    if (input[0] > 0)
        var new_title := chardata.available_citizenship_titles[input[0]];
        var old_title := chardata.citizenship_title;
        chardata.citizenship_title := new_title;
        SetObjProperty(targ, "chardata", chardata);
        SendSysMessage(who, "Título de cidadania de " + targ.acctname + " alterado de '" + old_title + "' para '" + new_title + "'.");
        SendSysMessage(targ, "Seu título de cidadania foi alterado de '" + old_title + "' para '" + new_title + "'.");
    else
        SendSysMessage(who, "Operação cancelada.");
    endif

endprogram
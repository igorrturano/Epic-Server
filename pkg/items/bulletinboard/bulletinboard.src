// //$Id: bulletinboard.src 375 2006-06-17 19:26:32Z austinheilman $

/////////////////////////////////////////
// 2.0
//
// bulletinboard.src
// by Austin Heilman
// Austin@tsse.net
//
// Bulletin board script that stores posts in a
// datafile.
//
// Created: 9/04/2001 11:32PM
// Updated: 2/13/2003 12:16PM
//
// History
// Feb. 13, 2003 - Austin
// A lot of code cleanup.
// Reading posts now uses the new gumps.inc function OldGFHTMLArea()
//
// Sep. 04, 2001 - Austin
// Package is created.
/////////////////////////////////////////

use uo;
use os;

include ":datafile:datafile";
include ":gumps:include/old/old-gumps";
include ":attributes:include/advanceCheck";

const ADD_POST_BTN := 1000;
const DELETE_BTN := 1001;

var board_data_file;
var board_elem;

program BulletinBoard(who, board)
	var input := 0;
	repeat
		board_data_file := DFOpenDataFile("MessageBoards", DF_CREATE);
		board_elem := DFFindElement(board_data_file, hex(board.serial), DF_CREATE);

		OldGFInitGump();

		OldGFButtonPage(40, 195, 0x1B6B, 0x1B6B, 500);
		OldGFGumpPic(20, 20, 0x87A);
		OldGFTextMid(260, 57, 0, GF_STDCOLOR, "Mural de Recados");

		BoardIndex(who, board);

		var subject := "", msg_lines := array;
		NewPost(who, subject, msg_lines);

		input := OldGFSendGump(who);
		//if ( !GetObjProperty(who, "#Online") )
		//	//If the player goes offline, terminate the script.
		//	exit;
		//endif


		if ( input[ADD_POST_BTN] )
			subject := OldGFExtractData(subject.keyid, input);
			//Take the 'lines' from the post and put them into 1 string.
			var msg_string := "";
			foreach line in msg_lines
				line := OldGFExtractData(line.keyid, input);
				if ( line )
					msg_string := msg_string+" "+line;
				endif
				sleepms(5);
			endforeach
			
			SendSysMessage(who, "type: "+ typeof(subject)); 

			var post_num := CInt(board_elem.GetProp("NextPost"));
			var new_num := CInt(board_elem.GetProp("NextPost"))+1;
			if (new_num > 50)
				//Were on post 50, reset the counter.
				new_num := 1;
			endif
			board_elem.SetProp("NextPost", new_num);
			var post_info := struct;
			post_info.+Name := who.name;
			post_info.+Subject := Subject;
			post_info.+Message := msg_string;
			post_info.+Data := polcore().systime;
			post_info.+Serial := who.serial;
			post_info.+GMPage := who.cmdlevel;
			board_elem.SetProp(hex(post_num), post_info);
		elseif ( input[0] >= DELETE_BTN )
			var temp := CInt(input[0]-DELETE_BTN);
			board_elem.EraseProp(hex(temp));
		endif
	until (input[0] <= 2);
	//Right click closes the gump.

endprogram

function BoardIndex(who, board)
	board := board;
	var y_pos := 180;
	var page_num := 1;
	var post_page_num := 1000;
	var counter := 1;
	var total := 0;
	var prop_list := board_elem.PropNames();
	foreach postnum in prop_list
		if ( postnum != "NextPost" )
			var name := board_elem.GetProp(postnum).Name;
			var subject := board_elem.GetProp(postnum).Subject;
			var message := board_elem.GetProp(postnum).Message;
			var serial := board_elem.GetProp(postnum).Serial;
			var tempo := CalculateDaysInReal( board_elem.GetProp(postnum).Data );
			
			OldGFPage(page_num);
			if ( tempo >= 15 && board_elem.GetProp(postnum).GMPage < 1 && !GetObjProperty(board, "NoTime"))
				board_elem.EraseProp(hex(cint(postnum)));
			else
				OldGFButtonPage(135, y_pos+3, 0xFC1, 0xFC1, post_page_num);
				OldGFTextLine(155, y_pos, GF_STDCOLOR, subject);
				y_pos := y_pos+20;
			endif

			OldGFPage(post_page_num);

			var msg := "<basefont color=#111111>"
				+"De: "+name+"<br>"
				+"Tempo: "+tempo+" dias<br>"
				+"Assunto: "+subject+"<br>"
				+"Mensagem:<br>";

			if ( TypeOf(message) == "Array" )
				foreach line in message
					msg := msg+line+" ";
					sleepms(2);
				endforeach
			else
				msg := msg+" "+message;
			endif
			OldGFHTMLArea(130, 180, 263, 160, 0, 1, msg);

			post_page_num := post_page_num+1;

			OldGFButtonPage(135, 367, 0x15E3, 0x15E7, page_num);
			OldGFTextLine(155, 365, GF_STDCOLOR, "Index");
			
			if (who.cmdlevel >= 1 || who.serial == serial || who.serial == GetObjProperty(board, "fixer") )
				OldGFTextLine(350, 364, GF_STDCOLOR, "Delete");
				OldGFButtonID(395, 367, 2117, 2118, 1, (CInt(postnum)+DELETE_BTN));
			endif

			counter := counter+1;
			total := total+1;

			if ( counter > 8 && total < (prop_list.size()-1) )
				OldGFPage(page_num);
				//Down arrow
				OldGFButtonPage(377, 336, 0xFC, 0xFD, page_num+1);
				page_num := page_num+1;
				OldGFPage(page_num);
				//Up arrow
				OldGFButtonPage(377, 160, 0xFA, 0xFB, page_num-1);
				y_pos := 180;
				counter := 1;
			endif
		endif
		sleepms(2);
	endforeach
endfunction

function NewPost(who, byref subject, byref msg_lines)
	OldGFPage(500);

	OldGFTextLine(130, 180, GF_STDCOLOR, "De: " + who.name);
	OldGFTextLine(130, 200, GF_STDCOLOR, "Assunto: ");
	subject := OldGFTextEntry(193, 200, 105, 20, GF_STDCOLOR, "Nova Mensagem");

	OldGFTextLine(130, 230, GF_STDCOLOR, "Mensagem:");

	var i;
	var y_pos := 250;
	//Five message lines.
	for (i:=0; i < 8/*5*/; i:=i+1)
		msg_lines.append(OldGFTextEntry(130, y_pos, 255, 20, GF_STDCOLOR));
		y_pos := y_pos+20;
	endfor

	OldGFButtonPage(135, 367, 0x15E3, 0x15E7, 1);
	OldGFTextLine(155, 365, GF_STDCOLOR, "Index");

	OldGFTextLine(355, 364, GF_STDCOLOR, "Post");
	OldGFButtonID(395, 367, 2117, 2118, 1, ADD_POST_BTN);
endfunction


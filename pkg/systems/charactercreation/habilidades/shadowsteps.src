use uo;
use os;
use polsys;

include ":attributes:attributes";
include "include/dungeons";
include ":timedScripts:timedScripts";
include ":tn:cooldown";
include "include/packets";
include "include/epicCombatUtils";

program sanctuary(params)
	var who := params[1];

    var duration := Cint(AP_GetSkill(who, MAGICLORE) / 5);
    if (TemHabilidade(who, "Caminhar nas sombras"))
        PlaySoundEffect(who, 0x688);
        if(InDarkness(who))
            TS_StartTimer(who, "velocidade", duration);
        endif
    endif

    if (TemHabilidade(who, "Avanco Sombrio"))
        if(InDarkness(who))
            var duration_shadowdancer := cint(AP_GetSkill(who, MAGICLORE)/2.5);
            Setcooldown(who, "shadowdancer_blur", duration_shadowdancer);
            TS_StartTimer(who, "spectralform", duration_shadowdancer);
        endif
    endif

    var i := 0;
    PlayObjectCenteredEffectHuefx(who, 0x54CE, 10, 20, 1420, 1);
    PlaySoundEffect(who, 0x204);
    if (who.npctemplate)
        who.concealed := 1;
        moveToBehind(who, who.opponent);
        sleep(1);
        who.concealed := 0;
    endif
	while (protected(who) && i < duration)
        who.concealed := 1;
		sleep(1);
        i++;
	endwhile

    who.concealed := 0;
endprogram

function protected(who)
    if (who.warmode && !TemHabilidade(who, "Forma Umbral"))
        SendSysMessage(who, "Voce entrou em modo de combate, saindo das sombras");
		return 0;
    elseif(GetObjProperty(who, "truesight"))
        SendSysMessage(who, "Voce foi revelado");
        EraseObjProperty(who, "truesight");
		return 0;
	endif

	return 1;
endfunction
 use uo;

include ":attributes:attributes";
include ":timedScripts:timedScripts";
include "include/say";

program manobra(attacker)

	//var hostiles := ListHostiles(attacker, 15, LH_FLAG_LOS);
	var hostiles := ListHostiles(attacker, 20);
	if (hostiles[1])
		SendSysMessageEx(attacker, "Voce nao pode usar essa habilidade enquanto esta sendo atacado.", SSM_FAIL);
		return;
	endif

	SendSysMessageEx(attacker, "Voce forjou sua morte, fique parado  ou voce sera revelado!", SSM_INFO);
	SetObjProperty(attacker, "forjoumorte", 1);
	var x := attacker.x;
	var y := attacker.y;
	var z := attacker.z;
    var r := attacker.realm;
	var mana := AP_GetVital(attacker, MANA);
	var hits := AP_GetVital(attacker, HITS);
	var stamina := AP_GetVital(attacker, STAMINA);

	attacker.kill();
	detach();
	sleep(2);
	Resurrect(attacker);
	EraseObjProperty(attacker, "forjoumorte");
	attacker.concealed := 1;
	attacker.squelch(-1);
	var ressrobe := GetEquipmentByLayer(attacker, 0x16);
	DestroyItem(ressrobe);
	var e := MoveObjectToLocation(attacker, x, y, z, r, MOVEOBJECT_FORCELOCATION);
	var corpse := attacker.getCorpse();

	while( attacker.connected )

		if( (attacker.x != corpse.x) && (attacker.y != corpse.y) )
			SendSysMessageEx(attacker, "Voce nao esta mais no local inicial", SSM_FAIL);
			break;
		endif
	

		sleep(1);

	endwhile

	attacker.concealed := 0;
	attacker.squelch(0);


	var serialC := attacker.serial;
	foreach item in EnumerateItemsInContainer(corpse)
		if (item.container == corpse)
			MoveObjectToLocation(item, attacker.x, attacker.y, attacker.z, attacker.realm);
			MoveItemToContainer(item, attacker.backpack);
			if ( CInt(GetObjProperty(item, "#EquippedOn")) == serialC )
				if ( !EquipItem(attacker, item) )
					EraseObjProperty(item, "#EquippedOn");
				endif
			endif
		endif
	endforeach

	DestroyItem(corpse);
	AP_SetVital(attacker, HITS, hits);
	AP_SetVital(attacker, MANA, mana);
	AP_SetVital(attacker, STAMINA, stamina);
endprogram  


use uo;
use util;
use os;
use cfgfile;

include ":fls_core:fls_characters";
include ":nature:nature";
include ":death:death";
include ":commands:debugsystem";

program daynightsystem()
	SetGlobalProperty("daynightpid", getpid());
	var config := readconfigfile(":nature:config/daynight");
	config := config["settings"];
	var darkness := cint(config.darkness);
	var transspeed := cint(config.transspeed);
	
	var i;
	while(true)
		var daylength := CInt(config.daylength) * 60;
		var fullDay := daylength*2;
		var nightlength;

		SeasonRotate();  //muda o dia do ano e estação
		MoonFase();      // muda a fase da lua
		// Temperature();   //mmuda a temperatura local

		var season := getSeason();
		var durationDayMod := 1;
		var date := getCurrentDate();

		var DEBUGTYPE := "clima";
		DebugMsgEvent(DEBUGTYPE, "Dia do ano: " + date.dayYear);
		DebugMsgEvent(DEBUGTYPE, "Dia: " + date.day);
		DebugMsgEvent(DEBUGTYPE, "Mes: " + date.month);
		DebugMsgEvent(DEBUGTYPE, "Ano: " + date.year);
		DebugMsgEvent(DEBUGTYPE, "Lua: " + date.moonPhase);
		DebugMsgEvent(DEBUGTYPE, "Estacao: " + season);

		if (date.month == "Idrithae")
			durationDayMod -= 0.3;
		endif

		if (season == SUMMER) //Checa se é solstício de verão ou inverno
			DebugMsgEvent(DEBUGTYPE, "Estacao: SUMMER");
			DebugMsgEvent(DEBUGTYPE, "Eh Solsticio de: SUMMER");
			durationDayMod += 0.2;
		elseif (season == WINTER)
			DebugMsgEvent(DEBUGTYPE, "Estacao: WINTER");
			DebugMsgEvent(DEBUGTYPE, "Eh Solsticio de: WINTER");
			durationDayMod -= 0.2;
		endif

		daylength := daylength*durationDayMod;
		nightlength := fullDay-daylength;

		DebugMsgEvent(DEBUGTYPE, "Duracao do dia: " + daylength / 60 + "min");
		DebugMsgEvent(DEBUGTYPE, "Duracao da noite: " + nightlength / 60 + "min");

		Broadcast("O sol esta nascendo.");
		SetGlobalProperty("dia", "dia");
		darkness := GeTMoonLightMod();
		//Aqui vai setando lentamente o lightlevel por região, é necessário passar aqui cada região (tybar, memmar e etc)
		for (i:=darkness;i>=0;i:=i-1)
			// setregionlightlevel("Umbraeterna", i);
			setregionlightlevel("Superficie", i);
			setglobalproperty("lightlevel", i);
			ApllyLightLevel();
			sleepms(transspeed);
		endfor
		setregionlightlevel("Umbraeterna", 24);

		SetGlobalProperty("halfdaytime", ReadGameClock());
		sleep(daylength);
  
		broadcast("O sol esta se pondo.");
		SetGlobalProperty("dia", "noite");
		//Aqui vai setando lentamente o lightlevel por região, é necessário passar aqui cada região (tybar, Superficie e etc)
		for (i:=0;i<=darkness;i:=i+1)
			// setregionlightlevel("Umbraeterna", i);
			setregionlightlevel("Superficie", i);
			setglobalproperty("lightlevel", i);
			ApllyLightLevel();
			sleepms(transspeed);
		endfor
		setregionlightlevel("Umbraeterna", 24);

		SetGlobalProperty("halfdaytime", ReadGameClock());
		sleep(nightlength);
		
		advanceDay();
	endwhile
endprogram


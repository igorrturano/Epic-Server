use uo;
use os;
use cfgfile;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";
include ":attributes:attributes";
include ":tn:npcutil";
include ":brainAI:npcUtil";
include ":ghaia:ghaia";
include ":spells:summon";


program SummonMagicCreature(params)

	var who := params[1];
	var targ := params[2];
	var spell := params[3];
	var spellid := spell.id;
	params := 0; // No longer needed

	if(GetObjProperty(who, "metamorfose") != error)
		SendSysMessageEx(who, "Voce ja esta metamorfoseado.", SSM_FAIL);
		return;
	endif

	PlaySoundEffect(targ, SFX_SPELL_HEAL);

	var cfg  :=  ReadConfigFile(":spells:spellcaster");
	var elem := FindConfigElem(cfg, spellid);
	if (elem == error)
		SendSysMessage(who, "Erro ao ler o template.");
		return;
    endif

	var graphic := getConfigInt(elem, "graphic");
	var skill_a := getConfigInt(elem, "skill_a");
	var skill_d := getConfigInt(elem, "skill_d");

	var dicedmg := getConfigString(elem, "dicedmg");
	var hitsmod := getConfigInt(elem, "hitsmod");
	var resistmod := getConfigInt(elem, "resistmod");
	var template := getConfigString(elem, "template");
	var snd := getConfigInt(elem, "snd");
	var namepoly:=GetConfigString(elem, "namepoly");

    var bonus := 0;
	if (TemHabilidade(who, "Instinto de Sobrevivência"))
        bonus := CINT(AP_GetSkill(who, THEOLOGY) / 5);
		skill_a := skill_a + bonus;
		skill_d := skill_d + bonus;
	endif


    var moon_fase := GetGlobalProperty("moonfase");
    if (graphic == 1070 && moon_fase == "cheia")
    	SetObjProperty(who, "polymorphbonus_moon", bonus);
		skill_a := skill_a + bonus;
		skill_d := skill_d + bonus;
        resistmod := resistmod + bonus;
        hitsmod := hitsmod + bonus;
    endif

    //Bonus de especialização
    if(Temhabilidade(who, "Treinamento Druídico"))
        skill_a := Cint(skill_a * 1.2);
        skill_d := Cint(skill_d * 1.2);
        resistmod := Cint(resistmod * 1.2);
        hitsmod := Cint(hitsmod * 1.2);
    endif

	//seta os mods
	SetObjProperty(who, "skill_a", skill_a);
	SetObjProperty(who, "skill_d", skill_d);
	SetObjProperty(who, "dicedmg", dicedmg);
	SetObjProperty(who, "polymorph_hitsmod", hitsmod);
	SetObjProperty(who, "hitsmod", hitsmod);
	SetObjProperty(who, "resistmod", resistmod);
	who.resist_physical_mod := who.resist_physical_mod + resistmod;
	who.resist_fire_mod := who.resist_fire_mod + resistmod;
	who.resist_cold_mod := who.resist_cold_mod + resistmod;
	who.resist_poison_mod := who.resist_poison_mod + resistmod;
	who.resist_energy_mod := who.resist_energy_mod + resistmod;

	recalcvitals(who);
	HealDamage(who, hitsmod);

	var metamorfosetime := 60 + 540 * AP_GetSkill(who, THEOLOGY) / 100;
	if(TemHabilidade(who,"Treinamento Druídico"))
		metamorfosetime := metamorfosetime * 2;
	endif

	Transforma(who, template, snd, graphic,namepoly);
	TS_StartTimer(who, "metamorfosedruida", metamorfosetime);

	return 1;
endprogram


function Transforma(who, animal, snd, graphic,namepoly)
	if (GetObjProperty(who,"charname") == error)
		SetObjProperty(who,"charname",who.name);
	endif

	if(TemHabilidade(who,"Elo Natural")) 
		UnequipArmor(who);
		UnequipWeapons(who);
	else //Desequipa tudo
		UnequipAll(who);
	endif

	PerformAction(who, ANIM_CAST_AREA);

	var npc := CreateNpcFromTemplate(animal, who.x,  who.y, who.z, 0, who.realm);
	who.frozen := 1;
	PrintText(who, "*transformando-se*");
	if (npc)
		//detach();
		Playsoundeffect(npc, snd);
		npc.facing := who.facing;
		npc.frozen := 1;
		npc.color  := 30000;
		PerformAction(npc, 5);
		sleep(2);
		MoveObjectToLocation(npc, 10, 10, 10, npc.realm,  MOVEOBJECT_FORCELOCATION);
		npc.kill();

        // Save the original appearance for reverting later
		var metamorfose := struct;
		var hair := GetEquipmentByLayer(who, LAYER_HAIR);
		var beard := GetEquipmentByLayer(who, LAYER_BEARD);
		var polymorphColor := CInt(hair.color);
		if(!polymorphColor)
			polymorphColor := CInt(beard.color);
		endif

		metamorfose.+originalcolor := who.color;
		metamorfose.+originalgraphic := who.graphic;
		metamorfose.+newgraphic := graphic;

		SetObjProperty(who, "metamorfose", metamorfose);

		who.graphic := graphic;
		who.color := polymorphColor;
	endif
	who.frozen := 0;
	SetName(who,namepoly);

	if (who.npctemplate)
		SetObjProperty(who, "runspeed", who.run_speed);
		who.run_speed := 200;
	endif
endfunction
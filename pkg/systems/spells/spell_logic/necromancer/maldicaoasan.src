use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";
include "include/epicMageryUtils";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	var spell := params[3];
	var spell_school := spell.School;
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		handleSilentMagic(who);
		return;
	endif

	handleSilentMagic(who, targ, SFX_SPELL_WEAKEN);

	var time := 0;
	var oldx := who.x;
	var oldy := who.y;

	who.frozen := 1;

	Corromper(who, 13, targ);
	var diff := handleSpellResistDiffForSpecialists(who, spell_school);
	var bonus := epicWisdomPathBonus(who);
	if (CheckResist(targ, VONTADE, diff, -bonus))
		SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
		who.frozen := 0;
		handleSilentMagic(who);
		return;
	endif

	targ.frozen := 1;

	while  (time < 10)

		if ( (who.x != oldx) || (who.y != oldy))
			SendSysMessageEx(who, "Voce se moveu e cancelou a magia.", SSM_FAIL);
			break;
		endif

		if (Distance(who, targ) >= 12)
			SendSysMessageEx(who, "O alvo se distanciou muito.", SSM_FAIL);
			break;
		endif

		var mydmg := cint(AP_GetVital(who, HITS)/10); //O calculo eh baseado somente no hp do caster

        if((AP_GetVital(who, HITS) - mydmg) <= 1)
            mydmg := AP_GetVital(who, HITS) - (AP_GetVital(who, HITS)-1);

            //Da o ultimo dano antes de parar o spell
            ApplyDamageEX(targ, mydmg, DMG_POISON);
            ApplyDamageEX(who, mydmg, DMG_POISON);

            SendSysMessageEx(who, "Voce usou toda sua forca vital.", SSM_FAIL);
            break;
        endif

		ApplyDamageEX(targ, mydmg, DMG_POISON);
        ApplyDamageEX(who, mydmg, DMG_POISON);

		if(targ.dead)
			break;
		endif

		PerformAction(who, ANIM_CAST_AREA);
		PlayObjectCenteredEffect(who, GFX_RED_SPARKLES, 7, 7);
		PlayObjectCenteredEffect(targ, GFX_RED_SPARKLES, 7, 7);
		time := time + 1;
		sleep(1);
	endwhile

	who.frozen := 0;
	targ.frozen := 0;

	return 1;
endprogram

include "include/say";
include ":timedScripts:timedScripts";
include ":attributes:attributes";
include "include/sysEvent";
include "include/epicMageryUtils";
include ":charactercreation:resists";

program SpellScript(params)
	var who := params[1];
    var targ := params[2];
    var spell := params[3];
    var spell_school := params[4];

    var duration := cint(AP_GetSkill(who, MAGICLORE)/20);
    if (TemHabilidade(who, "Valdruk"))
        duration := Cint(duration * 2);
    endif
    	
	if (!targ)
		SendSysMessageEx(who, "Cancelado.");
		return 1;
	endif

	if (!CheckLosAt(who, targ.x, targ.y, targ.z))
		SendSysMessageEx(who, "Você não enxerga o alvo.");
		return 1;
	endif

    PlayStationaryEffect(who.x, who.y, who.z, FX_SPARK_EFFECT, 2, 2);
    handleSilentMagic(who, targ, SFX_SPELL_STRENGTH);

    if (targ.acctname)
        var diff := handleSpellResistDiffForSpecialists(who, spell_school);
        var bonus := epicWisdomPathBonus(who);
        if (CheckResist(targ, VONTADE, diff, -bonus))
            SendSysMessageEx(who, "Você nota que o alvo nao foi afetado.", SSM_FAIL);
            return;
        endif
        SetObjProperty(targ, "#attackfail", 1);
        return;
    endif

	var globo := CreateNpcFromTemplate(":brainai:globodeluz", targ.x, targ.y, targ.z, 0, who.realm);
    createIllusion(globo, duration);
endprogram

function createIllusion(illusion, duration)
    illusion.name := "Ilusão";
    SetObjProperty(illusion, "NoCorpse", 1);

    var nearby := ListMobilesNearLocation( illusion.x ,illusion.y, illusion.z, 8, illusion.realm);
    var i := 0;

    while(i < duration)
        foreach char in nearby
            var event := struct{"type":=EVID_TAUNT, "source":=illusion};
            SendEvent(char, event);
        endforeach

        sleep(2);
        i++;
    endwhile
    SetObjProperty(illusion, "guardkill", 1);
    ApplyDamage(illusion, 9999);
endfunction
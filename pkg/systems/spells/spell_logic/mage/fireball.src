use uo;
include "include/tileEffects";
include ":attributes:attributes";
include ":fls_magery:spells";
include ":combat:damage";
include "include/shapes";
include ":timedScripts:timedScripts";
include "include/sysEvent";
include "include/client";
include "include/say"; 
include ":taming:taming";
include "include/epicConstants";
include "include/epicMageryUtils";
include ":destroyable:include/destroyItem";

program spell_fireball(params)
    var caster := params[1];
    var cast_on := params[2];
	var spell := params[3];
	var spell_circle := spell.Circle;
	var spell_school := spell.School;

	params := 0; // No longer needed

    SendSysMessage(caster, "Selecione o alvo.");
	//var cast_on := Target(caster);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := params[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "cancelado.", color := 33);
        return 0;
    endif

	var mlore := cint(AP_GetSkill(caster, ARCANA)/20);	
	var dist := Distance(caster, cast_on);
    var dist_allowed := distanceAllowedForSpells(caster, mlore, spell_school);

    if (dist > dist_allowed)
        SendSysMessage(caster, "O alvo esta muito distante");
        return 0;
    endif

    PlayMovingEffect(caster, cast_on, GFX_LARGE_FIREBALL, 10, 10, 1);

	var coord_list := GetCoordsInLine(caster.x, caster.y, cast_on.x, cast_on.y);
	foreach coord in coord_list
		if ( _coord_iter < 4 ) // Dont make one where its standing
			continue;
		endif
		
		var field := CreateItemAtLocation(coord.x, coord.y, caster.z, "FireField_NS", 1, caster.realm);
		field.SetOwner(caster);
		field.SetObjProperty(field,PROPNAME_INDESTRUTIVEL,1);
		field.SetDuration(0);
		sleepms(105);
	endforeach

	foreach coord in GetCoordsInSquare(cast_on.x, cast_on.y, 5)
		if ( RandomInt(2) != 1 ) // Don't place a fire field
			continue;
		endif
		case( RandomInt(3) )
			default:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_1, 10, 10, 0, caster.realm);
				break;
			1:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_2, 10, 10, 0, caster.realm);
				break;
			2:
				PlayStationaryEffect(coord.x, coord.y, caster.z, GFX_EXPLODE_3, 10, 10, 0, caster.realm);
				break;
		endcase
		var field;
		if ( RandomInt(2) )
			field := CreateItemAtLocation(coord.x, coord.y, cast_on.z, "FireFieldNS", 1, caster.realm);
		else
			field := CreateItemAtLocation(coord.x, coord.y, cast_on.z, "FireFieldEW", 1, caster.realm);
		endif
		field.SetOwner(caster);
		field.SetObjProperty(field,PROPNAME_INDESTRUTIVEL,1);
		field.SetDuration(1);
		sleepms(5);
	endforeach

	PlaySoundEffect(cast_on, 0x208);
    
	var range := cint(AP_GetSkill(caster, ARCANA)/20);
    var allMobiles := listAllMobilesNearbyButPetsAndParty(caster, range, cast_on);
	var dmg := epicSpellDamageRoll(caster, spell_circle);
	dmg := bonusSpellDamageForSpecialists(caster, dmg, spell_school);
	DanificaObjetosProximos(caster,cast_on.x, cast_on.y, cast_on.z,cast_on.realm,struct{dano := dmg, desc := "Bola de Fogo." },range,GFX_EXPLODE_1);

    foreach mobile in allMobiles
		var dmg := epicSpellDamageRoll(caster, spell_circle);

		dmg := bonusSpellDamageForSpecialists(caster, dmg, spell_school);

		if (Getcooldown(mobile, "spellthief"))
			DamageFLS(caster, cint(dmg), DMG_FIRE, caster, "fireball");
		else
			DamageFLS(mobile, cint(dmg), DMG_FIRE, caster, "fireball");
		endif

        sleepms(20);
    endforeach
	//destroycraft(cast_on, caster);
endprogram

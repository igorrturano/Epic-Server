    use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":attributes:regen";
include ":attributes:vitals";
include ":attributes:attributes";
include ":timedScripts:timedScripts";
include "include/epicMageryUtils";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	var spell := params[3];
	var spell_school := spell.School;
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif

    if (targ == who)
        SendSysMessageEx(who, "Precisa de dois para um pacto ser feito", SSM_FAIL);
		return;
    endif
    
	if (GetObjProperty(who, "pactodesepultura"))
		SendSysMessageEx(who, "Voce ja esta em um pacto de sepultura", SSM_FAIL);
		return;
	endif

	PlayMovingEffectEx( who, targ, 14202, 1, 0, 1001, 0, 0, 0, 0, 0, 0 );
	PlayMovingEffectEx( targ, who, 14202, 1, 0, 1001, 0, 0, 0, 0, 0, 0 );

	handleSilentMagic(who, who, 384);

	var diff := handleSpellResistDiffForSpecialists(who, spell_school);
	if (CheckResist(targ, VONTADE, diff, -CInt(AP_GetSkill(who, MAGICLORE)/2.5))) // Muito mais Dif√≠cil de resistir
		SendSysMessageEx(who, "Voce nota que o alvo nao foi afetado.", SSM_FAIL);
		SendSysMessageEx(targ, "Voce resistiu a magia.", SSM_INFO);
		return;
	endif

	var necrovital := AP_GetVitalMaximumValue(who, "Hits");

	SetObjProperty(who, "pactodesepulturadmg", necrovital);
	SetObjProperty(who, "pactodesepultura", targ.serial);

	SendSysMessageEx(who, "Voce e " + targ.name + " estao em um pacto de sepultura", SSM_INFO);
	SendSysMessageEx(targ, "Voce e " + who.name + " estao em um pacto de sepultura", SSM_INFO);
	Corromper(who, 15, targ);

	return 1;
endprogram

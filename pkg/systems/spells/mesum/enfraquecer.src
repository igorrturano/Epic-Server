    use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedScripts:timedScripts";
include ":charactercreation:resists";
include "include/epicMageryUtils";

program SpellScript(params)
	var who := params[1];
	var targ := params[2];
	var spell := params[3];
	var spell_school := spell.School;
	params := 0; // No longer needed

	if (!targ.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif

	var amt;
	var diff := handleSpellResistDiffForSpecialists(who, spell_school);
	if (!CheckResist(targ, VONTADE, diff, -CInt(AP_GetSkill(who, MAGICLORE)/2.5))) // Muito mais Dif√≠cil de resistir
		amt := Cint(AP_GetSkill(who, MAGICLORE)*0.5);
	else
		amt := Cint(AP_GetSkill(who, MAGICLORE)*0.25);
	endif

	if (temHabilidade(who, "Sacrificio da Carne"))
		if (isBloodied(who))
			amt := CInt(amt*1.5);
		endif
	endif

	TS_StartTimer(targ, "weaken", cint(AP_GetSkill(who, MAGICLORE)), amt, who, 1); //20 min
	PrintTextAbovePrivate(targ, "-"+amt + " Str", who, 3, 38); // vermelho

	handleSilentMagic(who, targ, SFX_SPELL_WEAKEN);

	SendSysMessageEx(who, "Voce deixou o alvo mais fraco.", SSM_INFO);
	Corromper(who, 1, targ);

	return 1;
endprogram

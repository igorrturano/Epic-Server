 
 
 
 use uo;
use os;

include ":spells:tnmagery";
include "include/client";
include "include/sounds";
include "include/say";
include ":timedscripts:timedScripts";
include ":tn:cooldown";
include "include/damage";
include "include/shapes";
include ":spells:swordmage";

program SpellScript(params)
	var who := params[1];
	var info := params[2];
	params := 0; // No longer needed

	//SendSysMessageEx(who, "Escolha sua arma.", SSM_REQUEST);
	var targ := who.weapon; //Spell_Target(who, TGTOPT_CHECK_LOS);
	if (!targ)
		SendSysMessageEx(who, "Cancelado", SSM_FAIL);
		return;
	endif

	var time := GetSpellDuration( AP_GetSkill(who, MAGICLORE) );	

	if (targ.isA(POLCLASS_WEAPON))

		var elemental := GetObjProperty(targ, "#elementalweapon");
		if (elemental.type != DMG_FIRE)
			SendSysMessageEx(who, "Esta arma precisa estar encantada com Lamina Flamejante.", SSM_FAIL);
			return 1;
		endif

		if (!GetCooldown(targ, "circulodevas") )
			SendSysMessageEx(who, "Esta arma precisa estar encantada com Circulo de Devastacao.", SSM_FAIL);
			return 1;
		endif

		releasegems(who, targ);
		targ.hp := 0;
		SendSysMessage(who, targ.desc+" quebrou.");
		MoveObjectToLocation(targ, 1, 1, 1, who.realm, MOVEOBJECT_FORCELOCATION);
		SetObjProperty(targ, "broken", 1);
		SetObjProperty(targ, "realdesc", targ.desc);
		eraseobjproperty(targ, "gems");
		SetName(targ, targ.desc+" [danificado]");
		MoveItemToContainer(targ, who.backpack);

		//DestroyItem(targ);

		foreach coord in GetCoordsInSquare(who.x, who.y, 12)

			if ( RandomInt(2) != 1 ) // Don't place a fire field
				continue;
			endif

			case( RandomInt(3) )
				default:
					PlayStationaryEffect(coord.x, coord.y, who.z, GFX_EXPLODE_1, 10, 10, 0);
					break;

				1:
					PlayStationaryEffect(coord.x, coord.y, who.z, GFX_EXPLODE_2, 10, 10, 0);
					break;

				2:
					PlayStationaryEffect(coord.x, coord.y, who.z, GFX_EXPLODE_3, 10, 10, 0);
					break;
			endcase

			var field;
			if ( RandomInt(2) )
				field := CreateItemAtLocation(coord.x, coord.y, who.z, "FireFieldNS", 1);
			else
				field := CreateItemAtLocation(coord.x, coord.y, who.z, "FireFieldEW", 1);
			endif

			field.SetDuration(20);
			sleepms(5);
		endforeach

    		var i := 30;
    		while(i > 0)
		      var x := who.x + RandomInt(15) - 7;
      			var y := who.y + RandomInt(15) - 7;
      			var newtarg := CreateItemAtLocation(x, y, who.z, 0xeed, 1);
      			newtarg.graphic := 1;
      			PlayMovingEffectXYZ( newtarg.x, newtarg.y,(newtarg.z + 80), newtarg.x, newtarg.y, newtarg.z, 0x36d4, 0x18, 0x10, 0);
      			i := i - 1;
      			DestroyItem(newtarg);
      			sleepms(200);
    		endwhile

		foreach defender in (ListMobilesNearLocation( who.x, who.y, who.z, 12, who.realm ))
			if ( !(defender in who.party.members ) )
				if (defender.serial != who.serial)
      					PlayObjectCenteredEffect(defender, 0x36cb, 7, 0x10);
      					PlaySoundEffect(defender, 0x228);
					var dmg := cint(AP_GetSkill(who, MAGICLORE))/2;
					DamageFLS(defender, cint(dmg), DMG_FIRE, who);
      					//ApplyDamageEX(defender, cint(dmg), DMG_FIRE);
				endif
			endif
    		endforeach
	
		return 1;
	endif

	SendSysMessageEx(who, "Alvo invalido.", SSM_INFO);

endprogram

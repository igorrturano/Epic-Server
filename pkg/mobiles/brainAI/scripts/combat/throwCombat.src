/*
throwCombat - Katabrok
AI Settings necessÃ¡rias:
  throwHit (int sound)
  throwDamage (string dice)
  throwItem (int item)
  throwNum (int quantidade)
  throwRange (int dist)
AI Settings opcionais:
  throwMiss (int sound)
  throwAnim (int anim)
*/

use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:commonFunctions";
include ":attributes:attributes";
include "include/shapes";
include "include/client";
include "include/sounds";
include "include/facings";
include "include/damage";

program BrainNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.

	//Store it here so it does not have to do the math over and over.
	var flee_level := NPC_GetFleeLevel(npc, settings["FleeLevel"]);

	var opponent := event.source;
	var flee_mode := 0;

	AI_SetOpponent(npc, opponent);

	SetObjProperty(npc, "#Throws", settings["throwNum"]);

	while( npc )
		var dist := Distance(npc, opponent);
		if ( ShouldFlee(npc, flee_level, flee_mode) )
			flee_mode := 1;
			AI_Move(npc, opponent, NEMOVE_AWAY, NEMOVE_RUN, WAKEUP, 25);
		elseif ( DoneFighting(npc, opponent, dist) )
			if ( scripts.Exists("EndFight") )
				params := array{npc, "EndFight", opponent, settings, scripts};
				AI_StartNerve(npc, "EndFight", scripts["EndFight"].script, params);
			else
				AI_WarMode(npc, 0);
			endif
			AI_EndNerve(npc, nerve_name);
		elseif( dist > settings["throwRange"] )
			AI_Move(npc, opponent, NEMOVE_TOWARD, NEMOVE_RUN, WAKEUP, dist);
			throw(npc, opponent, dist, settings);
		elseif ( dist > 1);
			AI_Move(npc, opponent, NEMOVE_TOWARD, NEMOVE_RUN, WAKEUP, dist);
		else
			npc.facing := GetFacing(npc.x, npc.y, opponent.x, opponent.y);
		endif

		sleepms(400);
		AI_ClearThoughts(npc, CLR_NERVE);
	endwhile
endprogram

function DoneFighting(npc, opponent, byref dist)
	if ( opponent.dead )
		return 1;
	elseif ( !opponent )
		return 1;
	elseif ( opponent.hidden )
		return 1;
	elseif ( opponent.concealed > npc.cmdlevel )
		return 1;
	elseif ( dist > 20 )
		return 1;
	elseif ( dist > 10 && !CheckLineOfSight(npc, opponent) )
		return 1;
	endif

	return 0;
endfunction

function ShouldFlee(npc, byref flee_level, byref flee_mode)
	if ( AP_GetVital(npc, "Hits") < flee_level )
		return 1;
	endif

	return 0;
endfunction

function throw(npc, opponent, byref dist, settings)
	if ( dist < 3 || dist > 15 )
		return 0;
	elseif ( !CheckLineOfSight(npc, opponent) )
		return 0;
	elseif ( !CInt(GetObjProperty(npc, "#Throws")) )
		return 0;
	endif

	var next_shoot := CInt(GetObjProperty(npc, "#NextShoot"));
	if ( next_shoot > ReadGameClock() )
		return 0;
	endif

	npc.frozen := 1;
	sleepms(50);
	if (settings["throwAnim"])
		PerformAction(npc, settings["throwAnim"]);
	else
		PerformAction(npc, 4);
	endif
	sleep(1);
	PlayMovingEffect(npc, opponent, settings["throwItem"], 15);
	npc.frozen := 0;

	var item := CreateItemAtLocation(opponent.x, opponent.y, opponent.z, settings["throwItem"], 1);
	item.movable := 1;

	var chance := GetAttribute(npc, "Dexterity") - GetAttribute(opponent, "Dexterity") / 3;
	if (RandomInt(100) > chance) // Errou!
		PrintTextAbove(npc, "*Miss!*", _DEFAULT_TEXT_FONT, 38);
		if (settings["throwMiss"])
			PlaySoundEffect(npc, settings["throwMiss"]);
		else
			PlaySoundEffect(npc, 0x137);
		endif
	else
		ApplyDamageEX(opponent, RandomDiceRoll(settings["throwDamage"]), DMG_PHYSICAL);
		PlaySoundEffect(npc, settings["throwHit"]);
	endif

	SetObjProperty(npc, "#NextShoot", ReadGameClock()+5);
	SetObjProperty(npc, "#Throws", CInt(GetObjProperty(npc, "#Throws")) - 1);

	return 1;
endfunction


use uo;
use os;

include ":containers:storageAreas";
include ":containers:containers";
include ":gumps:requestGump";
include ":gumps:gumps";
include ":gumps:yesNo";
include "include/say";
include ":keys:key";
include ":tn:tngumps";

//Gump Constants
const BKGND_STONESLAB		:= 9200;  // Gray stone slab background
const BKGND_BLUESLAB		:= 3600;  // Raised edge (blocky)
const GF_STDCOLOR		:= 1890;

const MOEDAS			:= 47715;

const MOEDAS_COBRE		:= 0xba63; //id das moedas de cobre
const MOEDAS_PRATA		:= 0xba64; //id das moedas de prata
const MOEDAS_OURO		:= 0xba65; //id das moedas de ouro
const GOLD_COIN			:= 0xeed;  //id das moedas de ouro (outro tipo válido)

var item_cfg := ReadConfigFile(":*:itemdesc");


program MerchantMenu(params)

	var npc := params[1];
	var player := params[2];
//	var npc := player;

	if (player.hidden)
		player.hidden := 0;
		SendSysMessageEx (player, "Voce se revelou ao mercador.", SSM_FAIL);
	endif

	var gump := GFCreateGump(0, 0);
	GFPage(gump, 0);
	GFResizePic(gump, 0, 0, BKGND_BLUESLAB, 640, 480);
	GFResizePic(gump, 15, 15, BKGND_STONESLAB, 150, 450);

	var quartos := GetObjProperty(npc, "quartos");
	var taxa;
	var valortaxa;

	if (getObjProperty(npc, "taxap"))
		taxa := (getObjProperty(npc, "taxap"));
	endif

	var souInquilino := 0;
	var meuquarto := -1;
	var j := 1;
	foreach quarto in quartos
		if (quarto.inquilino == player.serial)
			souInquilino := 1;
			meuquarto := j;
			break;
		endif
		j := j + 1;
	endforeach

	GFAddButton(gump, 20, 33, 2117, 2118, GF_CLOSE_BTN,  200);
	GFTextLine(gump, 40, 30, GF_STDCOLOR, "Listar Quartos");


	if (souInquilino)
		GFAddButton(gump, 20, 83, 2117, 2118, GF_CLOSE_BTN, 100);
		GFTextLine(gump, 40, 80, GF_STDCOLOR, "Trocar Fechadura");

		GFAddButton(gump, 20, 133, 2117, 2118, GF_CLOSE_BTN, 400);
		GFTextLine(gump, 40, 130, GF_STDCOLOR, "Entregar Quarto");

	endif

	if (player.cmdlevel > 1)
		GFAddButton(gump, 20, 183, 2117, 2118, GF_CLOSE_BTN,  300);
		GFTextLine(gump, 40, 180, GF_STDCOLOR, "Adicionar Quarto");

		GFAddButton(gump, 20, 233, 2117, 2118, GF_CLOSE_BTN,  500);
		GFTextLine(gump, 40, 230, GF_STDCOLOR, "Cobrar Aluguel");

		GFAddButton(gump, 20, 283, 2117, 2118, GF_CLOSE_BTN,  600);
		GFTextLine(gump, 40, 280, GF_STDCOLOR, "Mural de Recados");

		if (!(npc.serial in getglobalproperty("InnKeeper")))
			GFAddButton(gump, 20, 333, 2117, 2118, GF_CLOSE_BTN,  700);
			GFTextLine(gump, 40, 330, GF_STDCOLOR, "Cobrar Automatico");
		endif

		GFAddButton(gump, 20, 433, 2117, 2118, GF_CLOSE_BTN,  900);
		GFTextLine(gump, 40, 430, GF_STDCOLOR, "Opcoes do locador");
	endif

	if (GetObjProperty(npc, "confiscados").Exists(""+player.serial))
		GFAddButton(gump, 20, 383, 2117, 2118, GF_CLOSE_BTN,  800);
		GFTextLine(gump, 40, 380, GF_STDCOLOR, "Objetos Confiscados");
	endif

	var y_pos := 45;
	var i := 1;
	foreach quarto in quartos
		var dono := SystemFindObjectBySerial(quarto.inquilino, SYSFIND_SEARCH_OFFLINE_MOBILES);
		if (dono == error) //checa por via das duvidas pra ver se deletaram
			quarto.inquilino := 0;
			SetObjProperty(npc, "quartos", quartos);
		endif
		if (quarto.inquilino == error || quarto.inquilino == 0 || player.cmdlevel >=3)
			GFAddButton(gump, 175, y_pos+3, 2117, 2118, 1, i);
		endif
		var color := 666;
		if (quarto.inquilino == error || quarto.inquilino == 0) //se for desocupado fica cor 195, alugado fica cor 2449
			color := GF_STDCOLOR;
		endif
		if (player.cmdlevel >= 3) //se for gm mostra quem alugou, caso esteja alugado.
			if (quarto.inquilino == error || quarto.inquilino == 0)
				GFTextLine(gump, 195, y_pos,  color,  "" + quarto.name + " [" + quarto.custo + " moedas]");
			else
				dono := SystemFindObjectBySerial(quarto.inquilino, SYSFIND_SEARCH_OFFLINE_MOBILES);
				var dononame := dono.name;
				if (dononame == "Desconhecido")
					dononame := GetObjProperty(dono, "realname");
				endif
				GFTextLine(gump, 195, y_pos, color,  "" + quarto.name + " [" + quarto.custo + " moedas] ["  + dononame + "]" );
			endif
		else
			GFTextLine(gump, 195, y_pos, color,  "" + quarto.name + " [" + quarto.custo + " moedas]");
		endif
		y_pos := y_pos + 15;
		i := i + 1;
	endforeach

	var input := GFSendGump(player, gump);

	if (input[0] == 300) //adicionar quarto
		SendSysMessageEx(player, "Escolha a PORTA do quarto.", SSM_REQUEST);
		var porta := Target(player);

		if (!porta.isDoor())
			SendSysMessageEx(player, "Alvo invalido.", SSM_FAIL);
		else
			var room := struct;
			room.+door := porta.serial;
			SendSysMessageEx(player, "Selecione o centro com mais visibilidade dentro do quarto.");
			var cent := TargetCoordinates(player);
			room.+centro := { cent.x, cent.y, cent.z };
			if ( YesNo(player, "Existe um centro em Z diferente?") )
				SendSysMessageEx(player, "Selecione o centro com mais visibilidade dentro do novo Z.",SSM_INFO);
				var cent2 := TargetCoordinates(player);
				room.+centro := { cent.x, cent.y, cent.z, cent2.x, cent2.y, cent2.z };
			endif
			room.+custo := cint(RequestGump(player, "Quanto vai ser cobrado em moedas de cobre?", 0, 1));
			room.+name := cstr(RequestGump(player, "Qual o nome do quarto?"));
			SendSysMessage(player, "O quarto foi cadastrado.");
			var quartos := GetObjProperty(npc, "quartos");
			if (quartos == error)
				quartos := array;
			endif
			quartos.append(room);
			SetObjProperty(npc, "quartos", quartos);
		endif
	elseif (input[0] == 100 ) //trocar fechadura
		var yes := yesno(player, "O chaveiro vai cobrar 100 moedas, deseja continuar? " );
		if (yes)
			var bank_region := GetObjProperty(npc, "City");
			if ( !bank_region )
				bank_region := CP_DEFAULT_AREA;
			endif
			if ( !debitoEmConta(bank_region, player, 100) )
				SendSysMessageEx(player, "Voce precisa de 100 moedas no banco.", SSM_FAIL);
			else
				var lockid := AllocLockId();
				var key := CreateItemInBackpack( player, "ironkey", 1 );
				var door := SystemFindObjectBySerial(quartos[meuquarto].door);
				SetObjProperty(door, "LockId", lockid);
				door.locked := 1;
				SetObjProperty( key, "LockId", lockid );
			endif
		endif
	elseif (input[0] == 400)//entregar quarto

		var yes := yesno(player, "Deseja devolver o quarto?  Seus itens serão deletados." );
		var quarto := quartos[meuquarto];
		if (yes)
			var cent := quarto.centro;
			if (cent[1])
				DestroyRentItens(cent[1], cent[2], cent[3], npc.realm);
				if (cent[4])
					DestroyRentItens(cent[4], cent[5], cent[6], npc.realm);
				endif
			endif
			var door := SystemFindObjectBySerial(quarto.door);
			Eraseobjproperty(door, "LockId");
			quartos[meuquarto].inquilino := 0;
			SetObjProperty(npc, "quartos", quartos);

		endif


	elseif (input[0] == 500) // cobrar aluguel

		var i := 1;
		foreach quarto in quartos
			var bank_region := GetObjProperty(npc, "City");
			if ( !bank_region )
				bank_region := CP_DEFAULT_AREA;
			endif
			var player2 := SystemFindObjectBySerial(quartos[i].inquilino, SYSFIND_SEARCH_OFFLINE_MOBILES);

			if ( !debitoEmConta(bank_region, player2, quartos[i].custo) )
				//var quarto := quartos[meuquarto];
				//foreach cont in (quarto.containers)
				//	foreach item in (enumerateitemsincontainer(cont))
				//		DestroyItem(item);
				//	endforeach
				//endforeach
				var door := SystemFindObjectBySerial(quarto.door);
				Eraseobjproperty(door, "LockId");
				quartos[i].inquilino := 0;
				SetObjProperty(npc, "quartos", quartos);
				SendSysMessageEx(player, "Player " + player2.name + " do quarto " + quarto.name + " nao teve dinheiro para pagar o aluguel.", SSM_FAIL);
			else
				SendSysMessageEx(player, "Player " + player2.name + " do quarto " + quarto.name + " pagou o aluguel.", SSM_INFO);
				if(getObjProperty(npc, "taxap"))
				valortaxa := ((quartos[i].custo * taxa) / 100);
					else
				valortaxa := (quartos[i].custo * 0.1);
				endif

				if (GetObjProperty(npc, "cofreAluguel"))
					var cofser := getObjProperty(npc, "cofreAluguel");
					var cofrereal := SystemFindObjectBySerial(cofser);
					CreateItemInContainer( cofrereal, "moedacobre", valortaxa );
				endif
				if (GetObjProperty(npc, "cofreImpostos"))
					var cofser1 := getObjProperty(npc, "cofreImpostos");
					var cofrereal1 := SystemFindObjectBySerial(cofser1);
					CreateItemInContainer( cofrereal1, "moedacobre", quartos[i].custo - valortaxa );
				endif
			endif
			i := i +1;
		endforeach

	elseif (input[0] == 600) //linkar mural
		SendSysMessageEx(player, "Escolha o Mural que o NPC anunciara a cobrança de alugueis.");
		var mural := Target(player, TGTOPT_NOCHECK_LOS+TGTOPT_NEUTRAL);
		if (mural.objtype != 0x1E5E && mural.objtype != 0x1E5F)
			SendSysMessageEx(player, "Isto nao e um BulletinBoard =[.", SSM_FAIL);
		else
			SetObjProperty(npc, "mural", mural.serial);
			SendSysMessageEx(player, "O Mural foi linkado ao NPC.", SSM_INFO);
		endif
	elseif (input[0] == 700) //adicionar na global
		var innkeepers := getglobalproperty("InnKeeper");
		if (innkeepers == error)
			innkeepers := array;
		endif
		if (npc.serial in innkeepers)
			SendSysMessageEx(player, "NPC ja existe.", SSM_FAIL);
		else
			innkeepers.Append(npc.serial);
			setglobalproperty("InnKeeper",innkeepers);
			SendSysMessageEx(player, "NPC adicionado.", SSM_INFO);
		endif
	elseif (input[0] == 800) //objetos confiscados
		var bank_region := GetObjProperty(npc, "City");
		if ( !bank_region )
			bank_region := CP_DEFAULT_AREA;
		endif
		if ( !debitoEmConta(bank_region, player, 150) )
			SendSysMessageEx(player, "Voce precisa de 150 moedas no banco para pegar os itens confiscados.", SSM_FAIL);
		else //cria a chave
			var confiscados := GetObjProperty(npc, "confiscados");
			var mypack := confiscados["" + player.serial];
			mypack := SystemFindObjectBySerial(mypack);
			confiscados.Erase(""+player.serial);
			SetObjProperty(npc, "confiscados", confiscados);
			if (mypack != error)
				MoveItemToContainer( mypack, player.backpack);
				SendSysMessageEx(player, "Voce recebe seus objetos de volta.",SSM_INFO);
			else
				SendSysMessageEx(player, "Algo deu errado, voce nao recebe seus objetos de volta.",SSM_FAIL);
			endif
		endif
	elseif (input[0] == 900) //Opções do NPC
		if (player.cmdlevel >= 3)
			innerKeeperOpt(npc, player);
		else
			SendSysMessage(player, "Apenas o lider da Faccao pode alterar essas opcoes");
		endif
	elseif (input[0] < 100 && input[0] >= 1) //alugar quarto

		if (souInquilino)
			SendSysMessageEx(player, "Voce ja tem um quarto.");
			return 1;
		endif

		if (player.cmdlevel >=3) //opcao de gm de remover quarto
			var gmmenu :=  RadioGump(player, 250, 325, "Escolha uma opcao", array{"Mudar Nome","Mudar Preco","Remover Quarto", "Mudar Centro", "Informacoes"});

			if (gmmenu == "Remover Quarto")
				quartos.erase(input[0]);
				setobjproperty(npc, "quartos", quartos);
				SendSysMessageEx(player, "Quarto removido, caso ja tenha sido alugado apague a prop da porta e remova os itens nos baus.", SSM_INFO);
				return 1;
			elseif (gmmenu == "Mudar Nome")
				quartos[input[0]].name := cstr(RequestGump(player, "Qual o nome do quarto?"));
				setobjproperty(npc, "quartos", quartos);
				SendSysMessageEx(player, "Nome Alterado para: " +quartos[input[0]].name ,SSM_INFO);
				return 1;
			elseif (gmmenu == "Mudar Preco")
				quartos[input[0]].custo := cint(RequestGump(player, "Quanto vai ser cobrado em moedas de cobre?", 0, 1));
				setobjproperty(npc, "quartos", quartos);
				SendSysMessageEx(player, "Preco Alterado para" +quartos[input[0]].custo,SSM_INFO);
				return 1;
			elseif (gmmenu == "Informacoes")
				var inq := SystemFindObjectBySerial(quartos[input[0]].inquilino, SYSFIND_SEARCH_OFFLINE_MOBILES);
				SendSysMessageEx(player, " Inquilino "+ inq.name + " Conta: " + inq.acctname);
			elseif (gmmenu == "Mudar Centro")
				SendSysMessageEx(player, "Selecione o centro com mais visibilidade dentro do quarto.",SSM_INFO);
				var cent := TargetCoordinates(player);
				quartos[input[0]].centro := { cent.x, cent.y, cent.z };
				if ( YesNo(player, "Existe um centro em Z diferente?") )
					SendSysMessageEx(player, "Selecione o centro com mais visibilidade dentro do novo Z.",SSM_INFO);
					var cent2 := TargetCoordinates(player);
					quartos[input[0]].centro := { cent.x, cent.y, cent.z, cent2.x, cent2.y, cent2.z };
				endif

				setobjproperty(npc, "quartos", quartos);
				SendSysMessageEx(player, "Centro Alterado.",SSM_INFO);
				return 1;
			endif
		endif

		var yes := yesNo(player, "Voce deseja alugar esse quarto por " +  quartos[input[0]].custo +" moedas");
		if (yes)

			var bank_region := GetObjProperty(npc, "City");
			if ( !bank_region )
				bank_region := CP_DEFAULT_AREA;
			endif
			if ( !debitoEmConta(bank_region, player, quartos[input[0]].custo) )
				SendSysMessageEx(player, "Voce nao tem moedas suficientes para pagar o aluguel.", SSM_FAIL);
			else //cria a chave
				quartos[input[0]].inquilino := player.serial;
				quartos[input[0]].data := polcore().systime;
				SetObjProperty(npc, "quartos", quartos); //seta prop
				var lockid := AllocLockId();
				var key := CreateItemInBackpack( player, "ironkey", 1 );
				SetObjProperty(quartos[input[0]].door, "LockId", lockid);
				var porta := SystemFindObjectBySerial(quartos[input[0]]);
				porta.locked := 1;
				SetObjProperty( key, "LockId", lockid );
			endif
		endif

	endif


endprogram



function debitoEmConta(bank_region, player, amount)

	var bank_box := CP_GetStorageContainerForMobile(player, bank_region, CP_NOCREATE);
	if (!bank_box)
		SendSysMEssage(player, "Voce nao tem banco");
		return 0;
	endif
	//Verifica o valos disnonível no banco
	Set_Critical(1);
	var recursosDisponiveis := CalculaRecursosDisponiveisBank(bank_box, MOEDAS_COBRE);
	SendSysMessage(player, "Valor inicial disponivel no banco: " + recursosDisponiveis[5]);
	if (recursosDisponiveis[5] < amount)
		SendSysMessageEx(player, "Falta dinheiro no banco para pagar o aluguel.");
		Set_Critical(0);
		return 0;
	else
		var totalPago := 0;
		var faltaPagar := amount;
		var dicionarioAtual := Dictionary;
		var recursos := recursosDisponiveis[5];
		var diferenca;
		var moeda;
		if (recursos >= faltaPagar)
			if (faltaPagar > 0) //Tenta pagar com os cobres
				dicionarioAtual := recursosDisponiveis[1];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					diferenca := faltaPagar - moeda.amount;
					if (diferenca > 0)
						totalPago += moeda.amount;
						faltaPagar -= moeda.amount;
						DestroyItem(moeda);
					else
						totalPago += faltaPagar;
						SubtractAmount(moeda, faltaPagar);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel");
						break;
					endif
				endforeach
			endif
			if (faltaPagar > 0) //Tenta pagar com as pratas
				dicionarioAtual := recursosDisponiveis[2];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					diferenca := faltaPagar - moeda.amount*10;
					if (diferenca > 0)
						totalPago += moeda.amount*10;
						faltaPagar -= moeda.amount*10;
						DestroyItem(moeda);
					else
						var troco := moeda.amount*10 - faltaPagar;
						DestroyItem(moeda);
						totalPago += faltaPagar;
						DarTrocoBank(player, bank_box, troco);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel.");
						break;
					endif
				endforeach
			endif
			if (faltaPagar > 0) //Tenta pagar com os ouros
				dicionarioAtual := recursosDisponiveis[3];
				foreach serial in dicionarioAtual //Verifica se da pra pagar só com moedas de cobre
					moeda := SystemFindObjectBySerial(_serial_iter);
					//print("Serial: "+ _serial_iter +" Quantidade: "+ serial);
					//print("Moeda: "+ moeda.amount +" ");
					//print("Falta Pagar: "+ faltaPagar +" ");
					diferenca := faltaPagar - moeda.amount*100;
					if (diferenca > 0)
						totalPago += moeda.amount*100;
						faltaPagar -= moeda.amount*100;
						DestroyItem(moeda);
					else
						//print("Vai pagar com ouro");
						var troco := moeda.amount*100 - faltaPagar;
						DestroyItem(moeda);
						totalPago += faltaPagar;
						DarTrocoBank(player, bank_box, troco);
						faltaPagar := 0;
						SendSysMessage(player, "Pagou o aluguel!");
						break;
					endif
				endforeach
			endif
		else
			SendSysMessage(player, "Voce nao tem moedas suficientes no banco para pagar o aluguel.");
			Set_Critical(0);
			return 0;
		endif
	endif
	recursosDisponiveis := CalculaRecursosDisponiveisBank(bank_box, MOEDAS_COBRE);
	SendSysMessage(player, "Valor final disponivel no banco: " + recursosDisponiveis[5]);
	Set_Critical(0);
	return 1;
endfunction

function DestroyRentItens(x, y, z, r)
	var objects := ListItemsNearLocation( x, y, z, 10, r);
	foreach obj in objects
		if (CheckLosBetween( x, y, z, obj.x, obj.y, obj.z, r))
			if (GetObjProperty(obj, "crafterserial"))
				DestroyItem(obj);
			elseif (KP_IsLockable(obj))
				var container := EnumerateItemsInContainer( obj, ENUMERATE_IGNORE_LOCKED );
				foreach item in container
					DestroyItem(item);
					sleepms(20);
				endforeach
			endif
		endif
		sleepms(20);
	endforeach
	return 1;
endfunction



function CalculaRecursosDisponiveisBank(bank_box, cost_type)
	var recursosDisponiveis := Dictionary;
	recursosDisponiveis[1] := Dictionary; //Cobre
	recursosDisponiveis[2] := Dictionary; //Prata
	recursosDisponiveis[3] := Dictionary; //Ouro
	recursosDisponiveis[4] := Dictionary; //Outros
	recursosDisponiveis[5] := 0; //Total

	foreach item in EnumerateItemsInContainer(bank_box)
		if (item.objtype == MOEDAS_COBRE)
			recursosDisponiveis[1][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount;
		elseif (item.objtype == MOEDAS_PRATA)
			recursosDisponiveis[2][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount*10;
		elseif (item.objtype == MOEDAS_OURO || item.objtype == GOLD_COIN)
			recursosDisponiveis[3][item.serial] := item.amount;
			recursosDisponiveis[5] += item.amount*100;
		endif
	endforeach
	return recursosDisponiveis;
endfunction


function DarTrocoBank(player, bank_box, ValorTroco)
	var faltaPagar := ValorTroco;
	/*
	if(ValorTroco > 0)
		SendSysMessage(player, "Restam no banco:"); //Comentar quando for colocar em produção
	endif
	*/
	var ouro := CInt(faltaPagar/100);
	if(ouro > 0)
		CreateItemInContainer(bank_box, MOEDAS_OURO, ouro);
		//SendSysMessage(player, ouro + " moedas de ouro");
	endif
	faltaPagar -= CInt(ouro*100);

	var prata := CInt(faltaPagar/10);
	if(prata > 0)
		CreateItemInContainer(bank_box, MOEDAS_PRATA, prata);
		//SendSysMessage(player, prata + " moedas de prata");
	endif
	faltaPagar -= CInt(prata*10);

	var cobre := CInt(faltaPagar);
	if(cobre > 0)
		CreateItemInContainer(bank_box, MOEDAS_COBRE, cobre);
		//SendSysMessage(player, cobre + " moedas de cobre");
	endif
	return 1;
endfunction

function innerKeeperOpt(npc, player)

	var opt := array;
	if ( GetObjProperty(npc, "secret"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "onlygmsells"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "oneplayersells"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "startbuy") || GetObjProperty(npc, "endbuy"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "cofreAluguel"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "cofreImpostos"))
		opt.append(1);
	else
		opt.append(0);
	endif
	if ( GetObjProperty(npc, "Taxa") || GetObjProperty(npc, "taxap"))
		opt.append(1);
	else
		opt.append(0);
	endif
	/*
	if ( GetObjProperty(npc, "faccao") )
	opt.append(1);
	else
	opt.append(0);
	endif
	*/


	var result := CheckBoxGump(player, 350, 300, "Opcoes do Vendor", {"Secreto", "Apenas GM vende", "Apenas Um Player Vende", "Alterar Mensagens de Compra", "Cofre Aluguel", "Cofre Impostos", "Taxa"}, opt);

	EraseObjProperty(npc, "secret");
	EraseObjProperty(npc, "onlygmsells");
	EraseObjProperty(npc, "oneplayersells");
//	EraseObjProperty(npc, "startbuy");
//	EraseObjProperty(npc, "endbuy");

	foreach r in result
		if (r == "Secreto")
			SetObjProperty(npc, "secret", 1);
		elseif ( r == "Apenas GM vende")
			SetObjProperty(npc, "onlygmsells", 1);
		elseif ( r == "Apenas Um Player Vende")
			var oneplayer := cstr(RequestGump(player, "Qual o nome do player?"));
			SetObjProperty(npc, "oneplayersells", oneplayer);
		elseif ( r == "Alterar Mensagens de Compra")
			var startbuy := cstr(RequestGump(player, "Qual a mensagem de boas-vindas? [Cancele para deixar padrao]"));
			if (startbuy)
				SetObjProperty(npc, "startbuy", startbuy);
			endif
			var endbuy := cstr(RequestGump(player, "Qual a mensagem de despedida? [Cancele para deixar padrao]"));
			if (endbuy)
				SetObjProperty(npc, "endbuy", endbuy);
			endif
		elseif ( r == "Cofre Aluguel")
			SendSysMessageEx(player, "Escolha o cofre do aluguel", SSM_REQUEST);
			var cofre := Target(player);
			if (cofre.objtype == 40512 || cofre.objtype == 0x9AB || cofre.objtype == 0xE7C || cofre.objtype == 0xE41 || cofre.objtype == 0xE40 || cofre.objtype == 0xE43 || cofre.objtype == 0xE42)
				SetObjProperty(npc, "cofreAluguel", cofre.serial);
				SendSysMessageEx(player, "Os impostos do vendedor irao para este cofre, mantenha-o seguro", SSM_REQUEST);
			else
				SendSysMessageEx(player, "Escolha um bau grande o suficiente para recolher os impostos", SSM_REQUEST);
				return 1;
			endif
		elseif ( r == "Cofre Impostos")
			SendSysMessageEx(player, "Escolha o cofre dos impostos", SSM_REQUEST);
			var cofre := Target(player);
			if (cofre.objtype == 40512 || cofre.objtype == 0x9AB || cofre.objtype == 0xE7C || cofre.objtype == 0xE41 || cofre.objtype == 0xE40 || cofre.objtype == 0xE43 || cofre.objtype == 0xE42)
				SetObjProperty(npc, "cofreImpostos", cofre.serial);
				SendSysMessageEx(player, "Os impostos do vendedor irao para este cofre, mantenha-o seguro", SSM_REQUEST);
			else
				SendSysMessageEx(player, "Escolha um bau grande o suficiente para recolher os impostos", SSM_REQUEST);
				return 1;
			endif
		elseif ( r == "Taxa")
			var imposto := cint(RequestGump(player, "Insira a taxa cobrada em porcentagem [10 = 10%]"));
			SetObjProperty(npc, "taxap", imposto);
		endif

	endforeach

endfunction
use uo;
use os;
use util;
use cfgfile;

include ":fls_core:packets";
include ":brainAI:npcCommands";
include ":npcs:thor/handling/thorai";
include ":npcs:thor/handling/thorai";

program DblClickNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	var player	:= params[3].source;
	var settings	:= params[4];

	var serving := 0;
	var busy := 0;
	while ( npc )
		if ( player )
			if ( busy.pid )
				AI_Speak(npc, "Desculpe, mas estou ajudando outra pessoa.");
			else
				serving := player;
				if (GetObjProperty(npc, "startbuy"))
					AI_Speak(npc, ""+GetObjProperty(npc, "startbuy"));
				else
					var npcType := npc.npctemplate;
					var playerRaca := GetObjProperty(player, "chardata").raca;
					playerRaca := playerRaca.Lower();
					var sourceDescription := GetObjProperty(player, "descricao");
					var PlayerCultura := GetObjProperty(player, "chardata").culture;
					// print("PlayerCultura: "+PlayerCultura);

					if (playerRaca == "humano") // para Povos do Leste, Bjorske e Aiglanos serem diferenciados pelos NPCs
						playerRaca := PlayerCultura;
					endif

					var playerinput := "*"+ playerRaca + " " + sourceDescription + " se aproxima*";
					
					var responseAI := "A gente tem isso aqui... *aponta pra uma estante aos pedaços*";
					
					if ( npcType == ":merchants:taverneiro" || npcType == ":brainai:taverneiro" || npcType == "taverneiro" )
						npcType := "taverneiro"; // Get a propriedade "npc_type"
						responseAI := handle_doubleclick(npcType, playerinput);
					elseif ( npcType == ":merchants:merchant" || npcType == ":brainai:merchant" || npcType == "merchant" )
						npcType := "vendedor";
						responseAI := handle_doubleclick(npcType, playerinput);
					endif

					if (responseAI == "<uninitialized object>")
						responseAI := "Que procura? *funga forte o nariz, debruçando no balcão*";
					endif
					
					AI_Speak(npc, responseAI);
				endif
				busy := Start_Script(":merchants:pcmerchant/merchant_menu", {npc, player});
				if ( busy.errortext )
					AI_Speak(npc, "[Uh oh, I cant start my menu script! The error is ->"+busy.errortext+"]");
				endif
			endif
		endif

		var wait := 500;
		if ( busy.pid )
			if ( serving.hidden )
				SendSysMessage(serving, "Você se esconde e interrompe seu negócio com o mercador.");
				CloseGumpPacket(serving, busy.pid, 0);
				sleepms(500);
				busy.kill();
			elseif ( Distance(npc, serving) > 3 && serving.cmdlevel < 1 )
				SendSysMessage(serving, "Você desvia sua atenção da negociação com o mercador.");
				CloseGumpPacket(serving, busy.pid, 0);
				sleepms(500);
				busy.kill();
			else
				wait := 1;
			endif
		endif
		
		var ev := wait_for_event(wait);
		if ( ev )
			case ( ev.type )
				SYSEVENT_DOUBLECLICKED:
					player := ev.source;
				NPCEVENT_DONE:
					player := 0;
			endcase
		else
			player := 0;
		endif
	endwhile
endprogram

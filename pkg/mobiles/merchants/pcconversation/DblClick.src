use uo;
use os;
use util;
use cfgfile;

include ":fls_core:packets";
include ":brainAI:npcCommands";
include ":brainAI:npcUtil";

program DblClickNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var player	:= params[3].source;
	//var settings	:= params[4];

	var serving := 0;
	var busy := 0;
	//sendsysmessage(player, "clicou");
	while ( npc )
		if ( player )
			if ( busy.pid )
				AI_Speak(npc, "Desculpe, mas estou conversando com outra pessoa.");
			else
				serving := player;
				if (GetObjProperty(npc, "startbuy"))
					AI_Speak(npc, ""+GetObjProperty(npc, "startbuy"));
				else
					var npcType := npc.npctemplate;
					if ( npcType == ":merchants:taverneiro")
						npcType := "taverneiro engraçado"; // Get a propriedade "npc_type"
					else
						npcType := "vendedor";
					endif
					var language := "None";
					var event := "comercio";
					var mood := "None";
					var npcEvil := False;
					var gossip := False;
					var specialLore := False;

					var sourceDescription := GetObjProperty(player, "descricao");
					var playerinput := "<o personagem de descrição " + sourceDescription + " se aproxima do seu comércio>";
					
					var responseText := handle_open_ai_npc_speech(npcType, language, event, npcEvil, mood, gossip, specialLore, playerinput);
					AI_Speak(npc, responseText);
					//AI_Speak(npc, "Ola, no que posso ajuda-lo?");
				endif
				busy := Start_Script(":merchants:pcconversation/conversation", {npc, player});
				if ( busy.errortext )
					AI_Speak(npc, "[Uh oh, I cant start my menu script! The error is ->"+busy.errortext+"]");
				endif
			endif
		endif

		var wait := 500;
		if ( busy.pid )
			if ( serving.hidden )
				SendSysMessage(serving, "Você se escondeu e interrompeu a conversa com o NPC.");
				CloseGumpPacket(serving, busy.pid, 0);
				sleepms(500);
				busy.kill();
			elseif ( Distance(npc, serving) > 3 )
				SendSysMessage(serving, "Você se desvia sua atenção da conversa com o NPC.");
				CloseGumpPacket(serving, busy.pid, 0);
				sleepms(500);
				busy.kill();
			else
				wait := 1;
			endif
		endif
		
		var ev := wait_for_event(wait);
		if ( ev )
			case ( ev.type )
				SYSEVENT_DOUBLECLICKED:
					player := ev.source;
				NPCEVENT_DONE:
					player := 0;
			endcase
		else
			player := 0;
		endif
	endwhile
endprogram
 

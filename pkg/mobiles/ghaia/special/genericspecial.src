use uo;
use os;
use vitals;

include ":charactercreation:habilidades";
include ":timedScripts:timedScripts";
include "include/damage";
include "include/sysEvent";
include ":ghaia:generic";

program loko(params)
//print("entrou");
	var who := params[1];
	var event := params[2];

	if (!who.opponent)
		return;
	endif

	if (event.onlybloodied)
		if ( !IsBloodied(who) )
			return;
		endif
	endif

	//check distancia
	if (event.dist > 0)
		if (CoordinateDistance(who.x, who.y, who.opponent.x, who.opponent.y) > event.dist)
			return;
		endif
	endif

	//checa cooldown
	if ( TS_GetCooldown(who, event.name) != error)
		return;
	endif

	TS_StartCooldown(who, event.name, event.cd);
	//executa os states
	foreach state in (event.steps)

		EraseObjPRoperty(who, "noclosecombat");

		if (state.type == "Move")

			SetObjProperty(who, "noclosecombat", 1);
			var ev := struct;
			ev.type := EVID_COMBAT_MOVE;
			ev.x := state.x;
			ev.y := state.y;
			var loop := 1;
    			while (loop)
				SendEvent(who, ev);
            			if (CoordinateDistance( who.x, who.y, state.x, state.y ) <= 1 )
					EraseObjPRoperty(who, "noclosecombat");
					loop := 0;
				else
					loop := 1;
				endif
				sleepms(100);
    			endwhile

		elseif (state.type == "Teleport")
			if (state.target == "Self")
				MoveObjectToLocation(who, state.x, state.y, state.z, who.realm, MOVEOBJECT_FORCELOCATION);
			elseif (state.target == "Opponent")
				MoveObjectToLocation(who.opponent, state.x, state.y, state.z, who.realm, MOVEOBJECT_FORCELOCATION);
			elseif (state.target == "All Opponents")
				var opponents := GetAllOpponents(who);
				foreach op in opponents
					MoveObjectToLocation(op, state.x, state.y, state.z, who.realm, MOVEOBJECT_FORCELOCATION);
				endforeach
			endif
		elseif (state.type == "TimedScript")
			var targ := array;
			if (state.target == "Self")
				targ.append(who);
			elseif (state.target == "Opponent")
				targ.append(who.opponent);
			elseif (state.target == "All Opponents")
				targ := GetAllOpponents(who);
			endif
			foreach t in targ
				TS_StartTimer(t, state.timername, state.timerduration, state.level);
			endforeach
		elseif (state.type == "Perform Action" )
			var targ := array;
			if (state.target == "Self")
				targ.append(who);
			elseif (state.target == "Opponent")
				targ.append(who.opponent);
			elseif (state.target == "All Opponents")
				targ := GetAllOpponents(who);
			endif
			foreach t in targ
				PerformAction(t, state.id);
			endforeach
		elseif (state.type == "Text")
			var targ := array;
			if (state.target == "Self")
				targ.append(who);
			elseif (state.target == "Opponent")
				targ.append(who.opponent);
			elseif (state.target == "All Opponents")
				targ := GetAllOpponents(who);
			endif
			foreach t in targ
				PrintText(t, state.text, state.color);
			endforeach
		elseif (state.type == "Play Sound")
			PlaySoundEffect(who, state.id);
		elseif (state.type == "Delay")
			Sleep(state.delay);
		elseif (state.type == "Heal")
			  HealFLS(who, state.healamt);
			    RecalcVitals(who);
		elseif ( state.type == "Graphic Effect")
			var targ := array;
			if (state.target == "Self")
				targ.append(who);
			elseif (state.target == "Opponent")
				targ.append(who.opponent);
			elseif (state.target == "All Opponents")
				targ := GetAllOpponents(who);
			endif
			if (state.target == "Fixed Loc")
				if (state.graphictype == "Stationary")

				elseif (state.graphictype == "Moving")
					var startx := Cint(state.startx) + RandomInt(2 * state.startradius) - state.startradius;
					var starty :=  Cint(state.starty) + RandomInt(2 * state.startradius) - state.startradius;
					var endx := Cint(state.endx) + RandomInt(2 * state.endradius) - state.endradius;
					var endy := Cint(state.endy) + RandomInt(2 * state.endradius) - state.endradius;
					PlayMovingEffectXYZ(startx,  starty, Cint(state.startz), endx, endy, Cint(state.endz), state.fxid, state.speed, state.loop);
				endif
			else
				foreach t in targ
				if (state.graphictype == "Stationary")
					if (state.fxid == "Lightning")
						PlayLightningBoltEffect(t);
					else
						PlayObjectCenteredEffect(t, state.fxid, state.speed, state.loops);
					endif
				elseif (state.graphictype == "Moving")
					if (state.direction == "Mob->Player")
						PlayMovingEffect(who, t, state.fxid, state.speed, state.loops);
					else
						PlayMovingEffect(t, who, state.fxid, state.speed, state.loops);
					endif
				endif
				endforeach
			endif

		elseif (state.type == "Dmg")
			var targ := array;
			if (state.target == "Self")
				targ.append(who);
			elseif (state.target == "Opponent")
				targ.append(who.opponent);
			elseif (state.target == "All Opponents")
				targ := GetAllOpponents(who);
			endif
			foreach vital in (state.vitals)
				foreach t in targ
					if (vital == "Hits")
						DamageFLS(t, cint(state.dmg), state.dmgtype, who);
					elseif (vital == "Stam")
						AP_ConsumeVital(t, STAMINA, state.dmg);
					elseif (vital == "Mana")
						AP_ConsumeVital(t, MANA, state.dmg);
					endif
				endforeach
			endforeach
		elseif (state.type == "Summon Npcs") // Summonar NPC
			var amt := 0;
			while (amt < state.amount)
				var npc;
				if (Cint(state.x) == 6 && CInt(state.y) == 6 && Cint(state.z) == 6)
					npc := CreateNpcFromTemplate( cstr(state.npctemplate), Cint(who.x) + RandomInt(2 * state.range) - state.range,
												  Cint(who.y) + RandomInt(2 * state.range) - state.range, Cint(who.z));
				else
					npc := CreateNpcFromTemplate( cstr(state.npctemplate), Cint(state.x) + RandomInt(2 * state.range) - state.range,
												  Cint(state.y) + RandomInt(2 * state.range) - state.range, Cint(state.z));
				endif
                if (npc != error)
					var duracaonpc := state.duracao;
					if (!duracaonpc)
						duracaonpc := 120;
					endif
					TS_StartTimer(npc, "summon", duracaonpc);
                	amt := amt + 1;
                endif
              	sleepms(2);
            endwhile

		endif
		sleepms(500);
	endforeach


endprogram


function GetAllOpponents(who)

	var a := ListHostiles(who);
	var mobs := ListMobilesNearLocation(who.x, who.y, LIST_IGNORE_Z, 12, who.realm);

	foreach mobile in mobs
		if (mobile.serial != who.serial)
			if ( !(mobile in a) )
				if (mobile.acctname || mobile.script == ":ghaia:tamed" )
					a.append(mobile);
				endif
			endif
		endif
	endforeach

	return a;


endfunction
